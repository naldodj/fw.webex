/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

namespace FWWebEx

#include "fw.webex.th"

class WebExCard from WebExDiv

    protected data cCardTextTitle as character
    protected data cCardTextBody as character

    protected data oCardBody as object
    protected data oCardTextTitle as object
    protected data oCardTextBody as object

    public method New(cCardTextTitle as character,cCardTextBody as character) as object

    public method SetCardTextTitle(cCardTextTitle as character) as object
    public method SetCardTextBody(cCardTextBody as character) as character

endclass

method New(cCardTextTitle,cCardTextBody) class WebExCard
    paramtype 1 var cCardTextTitle as character
    paramtype 2 var cCardTextBody as character optional default ""
    ::cCardTextTitle:=cCardTextTitle
    ::cCardTextBody:=cCardTextBody
    _Super:New()
    WITH WEBEXOBJECT self
        .:AddClass("card")
        WITH WEBEXOBJECT ::oCardBody CLASS WebExDiv
            .:AddClass("card-body")
            WITH WEBEXOBJECT ::oCardTextTitle CLASS WebExH5
                ::SetCardTextTitle(::cCardTextTitle)
            END WEBEXOBJECT
            if (!Empty(::cCardTextBody))
                WITH WEBEXOBJECT ::oCardTextBody CLASS WebExParagraph
                    ::SetCardTextBody(::cCardTextBody)
                END WEBEXOBJECT
            endif
        END WEBEXOBJECT
    END WEBEXOBJECT NOADDCHILD
return(self)

method SetCardTextTitle(cCardTextTitle) class WebExCard
    paramtype 1 var cCardTextTitle as character optional default ::cCardTextTitle
    ::SetMemberData("cCardTextTitle",cCardTextTitle)
    ::AddClass("card-title")
    ::SetContent(::cCardTextTitle)
return(self)

method SetCardTextBody(cCardTextBody) class WebExCard
    paramtype 1 var cCardTextBody as character optional default ::cCardTextBody
    ::SetMemberData("cCardTextBody",cCardTextBody)
    ::AddClass("card-text")
    ::SetContent(::cCardTextBody)
return(self)
