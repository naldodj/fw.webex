/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

namespace FWWebEx

#include "fw.webex.th"

class WebExFieldGroup from WebExControl

    protected data cFieldGroupLabel as character
    protected data cFieldGroupName as character
    protected data cFieldGroupType as character
    protected data cFieldGroupPlaceHolder as character
    protected data cFieldGroupTitle as character

    protected data nFieldGroupColSize

    protected data oFieldGroupWebExCol as object
    protected data oFieldGroupWebExLabel as object
    protected data oFieldGroupWebExControl as object
    protected data oFieldGroupWebExInputGroup as object

    public method New(cFieldGroupLabel as character,cFieldGroupName as character,cFieldGroupType as character,cFieldGroupPlaceHolder as character,cFieldGroupTitle as character,nFieldGroupColSize as numeric) as object

    public method SetFieldGroupLabel(cFieldGroupLabel as character) as object
    public method SetFieldGroupName(cFieldGroupName as character) as object
    public method SetFieldGroupType(cFieldGroupType as character) as object
    public method SetFieldPlaceHolder(cFieldGroupPlaceHolder as character) as object
    public method SetFieldGroupTitle(cFieldGroupTitle as character) as object
    public method SetFieldGroupColSize(nFieldGroupColSize as numeric) as object

endclass

method New(cFieldGroupLabel,cFieldGroupName,cFieldGroupType,cFieldGroupPlaceHolder,cFieldGroupTitle,nFieldGroupColSize) class WebExFieldGroup

    paramtype 1 var cFieldGroupLabel as character
    paramtype 2 var cFieldGroupName as character
    paramtype 3 var cFieldGroupType as character
    paramtype 4 var cFieldGroupPlaceHolder as character
    paramtype 5 var cFieldGroupTitle as character optional default cFieldGroupPlaceHolder
    paramtype 6 var nFieldGroupColSize as numeric optional default 4

    ::cFieldGroupLabel:=cFieldGroupLabel
    ::cFieldGroupName:=cFieldGroupName
    ::cFieldGroupType:=cFieldGroupType
    ::cFieldGroupPlaceHolder:=cFieldGroupPlaceHolder
    ::cFieldGroupTitle:=cFieldGroupTitle
    ::nFieldGroupColSize:=nFieldGroupColSize

    _Super:New("div")
    ::AddClass("mb-3")

    // Cria o input
    ::oFieldGroupWebExControl:=WebExControl():New("input")
    ::SetFieldGroupType()
    ::SetFieldGroupName()
    ::SetFieldPlaceHolder()
    ::SetFieldGroupTitle()

    // Cria o grupo que retornara por referencia
    ::oFieldGroupWebExInputGroup:=WebExInputGroup():New()
    ::oFieldGroupWebExInputGroup:AddChild(::oFieldGroupWebExControl)

    //Monta o Label
    ::oFieldGroupWebExLabel:=WebExLabel():New(::oFieldGroupWebExControl:GetFixedID(),::cFieldGroupLabel)
    ::SetFieldGroupLabel()

    // Monta estrutura
    ::oFieldGroupWebExCol:=WebExCol():New(::nFieldGroupColSize)
    ::SetFieldGroupColSize()
    ::oFieldGroupWebExCol:AddChild(::oFieldGroupWebExLabel)
    ::oFieldGroupWebExCol:AddChild(::oFieldGroupWebExInputGroup)

    ::AddChild(::oFieldGroupWebExCol)

return(self)

method SetFieldGroupLabel(cFieldGroupLabel) class WebExFieldGroup
    paramtype 1 var cFieldGroupLabel as character optional default ::cFieldGroupLabel
    ::SetMemberData("cFieldGroupLabel",cFieldGroupLabel)
    ::oFieldGroupWebExLabel:SetLabelFor(::oFieldGroupWebExControl:GetFixedID())
    ::oFieldGroupWebExLabel:SetLabelText(::cFieldGroupLabel)
return(self)

method SetFieldGroupName(cFieldGroupName) class WebExFieldGroup
    paramtype 1 var cFieldGroupName as character optional default ::cFieldGroupName
    ::SetMemberData("cFieldGroupName",cFieldGroupName)
    ::oFieldGroupWebExControl:SetAttr("name",::cFieldGroupName)
return(self)

method SetFieldGroupType(cFieldGroupType) class WebExFieldGroup
    paramtype cFieldGroupType as character optional default ::cFieldGroupType
    ::SetMemberData("cFieldGroupType",cFieldGroupType)
    ::oFieldGroupWebExControl:SetAttr("type",::cFieldGroupType)
    if (!::cFieldGroupType=="checkbox")
        ::oFieldGroupWebExControl:AddClass("form-control")
    else
        ::oFieldGroupWebExControl:DelClass("form-control")
    endif
return(self)

method SetFieldPlaceHolder(cFieldGroupPlaceHolder) class WebExFieldGroup
    paramtype 1 var cFieldGroupPlaceHolder as character optional default ::cFieldGroupPlaceHolder
    ::SetMemberData("cFieldGroupPlaceHolder",cFieldGroupPlaceHolder)
    if (!Empty(::cFieldGroupPlaceHolder))
        ::oFieldGroupWebExControl:SetAttr("placeholder",::cFieldGroupPlaceHolder)
    else
        ::oFieldGroupWebExControl:DelAttr("placeholder")
    endif
return(self)

method SetFieldGroupTitle(cFieldGroupTitle) class WebExFieldGroup
    paramtype 1 var cFieldGroupTitle as character optional default ::cFieldGroupTitle
    ::SetMemberData("cFieldGroupTitle",cFieldGroupTitle)
    if (!Empty(::cFieldGroupTitle))
        ::oFieldGroupWebExControl:SetAttr("title",::cFieldGroupTitle)
    else
        ::oFieldGroupWebExControl:DelAttr("title")
    endif
return(self)

method SetFieldGroupColSize(nFieldGroupColSize) class WebExFieldGroup
    paramtype 1 var nFieldGroupColSize as numeric optional default ::nFieldGroupColSize
    ::SetMemberData("nFieldGroupColSize",nFieldGroupColSize)
    ::oFieldGroupWebExCol:SetColSize(::nFieldGroupColSize)
return(self)
