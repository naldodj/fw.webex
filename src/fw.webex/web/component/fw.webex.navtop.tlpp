/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

Released to Public Domain.

local oNavTop:=WebExNavTop():New("FWWebEx") as object
oNavTop:AddLink("Home","#home",WebExIcon():New("bi-house"))
oNavTop:AddDropDown(;
    "Acoes";
    ,{;
        ,{"Salvar","#salvar"};
        ,{"Excluir","#excluir"};
        ,{"Sair","#sair"};
    };
)

--------------------------------------------------------------------------------------
*/

namespace FWWebEx

#include "fw.webex.th"

class WebExNavTop from WebExNav

    protected data cNavTopBrand as character

    protected data jNavTopLinks as json
    protected data jNavTopDropDowns as json

    protected data oNavTopLink as object
    protected data oNavTopLinkIcon as object

    protected data oNavTopDropdownItem as object
    protected data oNavTopDropdownMenu as object
    protected data oNavTopDropdownLink as object
    protected data oNavTopDropdownToggle as object
    protected data oNavTopDropdownWrapper as object

    protected data oNavTopBrand as object
    protected data oNavTopContainer as object

    protected data oNavTopSidebarToggle as object
    protected data oNavTopSidebarToggleIcon as object

    public method New(cNavTopBrand as character) as object
    public method AddLink(cNavTopLabel as character,cNavTopHRef as character,oNavTopLinkIcon as object) as object
    public method AddDropDown(cNavTopLabel as character,aNavTopDropDownItems as array) as object
    public method RenderHTML() as character

endclass

method New(cNavTopBrand) class WebExNavTop

    paramtype 1 var cNavTopBrand as character optional default ""

    ::cNavTopBrand:=cNavTopBrand
    ::jNavTopLinks:=JSONObject():New()
    ::jNavTopDropDowns:=JSONObject():New()

    _Super:New()
    ::AddClass("navbar mb-4")

    // Botao para abrir/recolher o menu lateral (sidebar)
    ::oNavTopSidebarToggle:=WebExButton():New("","")
    ::oNavTopSidebarToggle:DelAllAttr()
    ::oNavTopSidebarToggle:SetAttr("class","navbar-toggler border-0 bg-transparent mb-1 ps-0 pe-0 ms-0 me-0")
    ::oNavTopSidebarToggle:SetAttr("type","button")
    ::oNavTopSidebarToggle:SetAttr("data-bs-toggle","collapse")
    ::oNavTopSidebarToggle:SetAttr("data-bs-target","#webex-sidebar")
    ::oNavTopSidebarToggle:SetAttr("aria-controls","webex-sidebar")
    ::oNavTopSidebarToggle:SetAttr("aria-expanded","false")
    ::oNavTopSidebarToggle:SetAttr("aria-label","Alternar menu")
    ::oNavTopSidebarToggle:SetAttr("style","margin-bottom: 2px; outline:none; box-shadow:none;")

    ::oNavTopSidebarToggleIcon:=WebExIcon():New("bi-list")
    ::oNavTopSidebarToggleIcon:AddClass("fs-4 ms-0 me-0")
    ::oNavTopSidebarToggle:AddChild(::oNavTopSidebarToggleIcon)

    // Marca do sistema
    ::oNavTopBrand:=WebExAnchor():New()
    ::oNavTopBrand:SetAttr("class","navbar-brand ps-0 pe-0 ms-0 me-0")
    ::oNavTopBrand:SetAttr("href","#")
    ::oNavTopBrand:SetContent(::cNavTopBrand)

    // Container principal do topo
    ::oNavTopContainer:=WebExDiv():New()
    ::oNavTopContainer:SetAttr("class","container-fluid d-flex flex-column align-items-start px-0")

    ::oNavTopContainer:AddChild(::oNavTopSidebarToggle)
    ::oNavTopContainer:AddChild(::oNavTopBrand)

    ::AddChild(::oNavTopContainer)

return(self)

method AddLink(cNavTopLabel,cNavTopHRef,oNavTopLinkIcon) class WebExNavTop
    paramtype 1 var cNavTopLabel as character
    paramtype 2 var cNavTopHRef as character
    paramtype 3 var oNavTopLinkIcon as object class WEBEXICON optional
    ::oNavTopLink:=WebExAnchor():New()
    ::oNavTopLink:SetAttr("class","nav-link")
    ::oNavTopLink:SetAttr("href",cNavTopHRef)
    if (oNavTopLinkIcon!=nil)
        ::oNavTopLinkIcon:=oNavTopLinkIcon
        ::oNavTopLink:AddChild(::oNavTopLinkIcon)
        ::oNavTopLink:SetContent(" ")
    endif
    ::oNavTopLink:SetContent(cNavTopLabel)
    ::jNavTopLinks[::oNavTopLink:GetID()]:=::oNavTopLink
return(self)

method AddDropDown(cNavTopLabel,aNavTopDropDownItems) class WebExNavTop

    local i as numeric

    ::oNavTopDropdownToggle:=WebExAnchor():New()
    ::oNavTopDropdownToggle:SetAttr("class","nav-link dropdown-toggle")
    ::oNavTopDropdownToggle:SetAttr("href","#")
    ::oNavTopDropdownToggle:SetAttr("role","button")
    ::oNavTopDropdownToggle:SetAttr("data-bs-toggle","dropdown")
    ::oNavTopDropdownToggle:SetAttr("aria-expanded","false")
    ::oNavTopDropdownToggle:SetContent(cNavTopLabel)

    ::oNavTopDropdownMenu:=WebExControl():New("ul")
    ::oNavTopDropdownMenu:SetAttr("class","dropdown-menu")
    for i:=1 to Len(aNavTopDropDownItems)
        ::oNavTopDropdownItem:=WebExLI():New()
        ::oNavTopDropdownLink:=WebExAnchor():New()
        ::oNavTopDropdownLink:SetAttr("class","dropdown-item")
        ::oNavTopDropdownLink:SetAttr("href",aNavTopDropDownItems[i][2])
        ::oNavTopDropdownLink:SetContent(aNavTopDropDownItems[i][1])
        ::oNavTopDropdownItem:AddChild(::oNavTopDropdownLink)
        ::oNavTopDropdownMenu:AddChild(::oNavTopDropdownItem)
    next i

    ::oNavTopDropdownWrapper:=WebExLI():New()
    ::oNavTopDropdownWrapper:SetAttr("class","nav-item dropdown")
    ::oNavTopDropdownWrapper:AddChild(::oNavTopDropdownToggle)
    ::oNavTopDropdownWrapper:AddChild(::oNavTopDropdownMenu)

    ::jNavTopDropDowns[::oNavTopDropdownWrapper:GetID()]:=::oNavTopDropdownWrapper

return(self)

method RenderHTML() class WebExNavTop

    local cHTML as character

    local oUl as object
    local oCollapse as object

    if ((Len(::jNavTopLinks:GetNames())>0).or.(Len(::jNavTopDropDowns:GetNames())>0))
        oUl:=WebExUL():New()
        oUl:SetAttr("class","navbar-nav me-auto mb-0")
        // Adiciona os links
        aEval(::jNavTopLinks:GetNames(),{|k|oUl:AddChild(::jNavTopLinks[k])})
        // Adiciona os dropdowns
        aEval(::jNavTopDropDowns:GetNames(),{|k|oUl:AddChild(::jNavTopDropDowns[k])})
        // Menu colapsavel da barra superior
        oCollapse:=WebExDiv():New()
        oCollapse:SetAttr("class","collapse navbar-collapse p-0 m-0")
        oCollapse:SetFixedID(::GetFixedID()+"_menu")
        oCollapse:AddChild(oUl)
        ::oNavTopContainer:AddChild(oCollapse)
    endif

    cHTML:=_Super:RenderHTML()

return(cHTML)
