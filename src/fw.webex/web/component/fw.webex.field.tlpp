/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

namespace FWWebEx

#include "fw.webex.th"

class WebExField from WebExControl

    protected data cFieldLabel as character
    protected data cFieldName as character
    protected data cFieldType as character
    protected data cFieldPlaceHolder as character
    protected data cFieldTitle as character

    protected data oWebExInputField as object
    protected data oWebExLabelField as object

    public method New(cFieldLabel as character,cFieldName as character,cFieldType as character,cFieldPlaceHolder as character,cFieldTitle as character) as object

    public method SetFieldLabel(cFieldLabel as character) as object
    public method SetFieldName(cFieldName as character) as object
    public method SetFieldType(cFieldType as character) as object
    public method SetFieldPlaceHolder(cFieldPlaceHolder as character) as object
    public method SetFieldTitle(cFieldTitle as character) as object

endclass

method New(cFieldLabel,cFieldName,cFieldType,cFieldPlaceHolder,cFieldTitle) class WebExField
    paramtype 1 var cFieldLabel as character
    paramtype 2 var cFieldName as character
    paramtype 3 var cFieldType as character
    paramtype 4 var cFieldPlaceHolder as character
    paramtype 5 var cFieldTitle as character optional default cFieldPlaceHolder
    ::cFieldLabel:=cFieldLabel
    ::cFieldName:=cFieldName
    ::cFieldType:=cFieldType
    ::cFieldPlaceHolder:=cFieldPlaceHolder
    ::cFieldTitle:=cFieldTitle
    _Super:New("div")
    ::AddClass("mb-3")
    ::oWebExInputField:=WebExInput():New(cFieldName,cFieldType,cFieldPlaceHolder,cFieldTitle)
    ::oWebExLabelField:=WebExLabel():New(::oWebExInputField:GetFixedID(),cFieldLabel)
    ::AddChild(::oWebExLabelField)
    ::AddChild(::oWebExInputField)
return(self)

method SetFieldLabel(cFieldLabel) class WebExField
    paramtype 1 var cFieldLabel as character optional default ::cFieldLabel
    ::SetMemberData("cFieldLabel",cFieldLabel)
    ::oWebExLabelField:SetLabelFor(::oWebExInputField:GetFixedID())
    ::oWebExLabelField:SetLabelText(::cFieldLabel)
return(self)

method SetFieldName(cFieldName) class WebExField
    paramtype 1 var cFieldName as character optional default ::cFieldName
    ::SetMemberData("cFieldName",cFieldName)
    ::oWebExInputField:SetInputName(::cFieldName)
return(self)

method SetFieldType(cFieldType) class WebExField
    paramtype cFieldType as character optional default ::cFieldType
    ::SetMemberData("cFieldType",cFieldType)
    ::oWebExInputField:SetInputType(::cFieldType)
return(self)

method SetFieldPlaceHolder(cFieldPlaceHolder) class WebExField
    paramtype 1 var cFieldPlaceHolder as character optional default ::cFieldPlaceHolder
    ::SetMemberData("cFieldPlaceHolder",cFieldPlaceHolder)
    ::oWebExInputField:SetInputPlaceHolder(::cFieldPlaceHolder)
return(self)

method SetFieldTitle(cFieldTitle) class WebExField
    paramtype 1 var cFieldTitle as character optional default ::cFieldTitle
    ::SetMemberData("cFieldTitle",cFieldTitle)
    ::oWebExInputField:SetInputTitle(::cFieldTitle)
return(self)
