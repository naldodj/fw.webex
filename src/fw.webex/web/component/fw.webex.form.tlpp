/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

namespace FWWebEx

#include "fw.webex.th"

class WebExForm from WebExControl

    protected data cFormTitle as character
    protected data cFormMethod as character
    protected data cFormAction as character

    protected data jFormFieldWebExFields as json
    protected data jFormWebExButtonButtons as json

    protected data lFormButtonsFirst as logical

    protected data nFieldGroupColSize as numeric

    protected data oFormWebExButton as object
    protected data oFormFieldsWebExRow as object
    protected data oFormFieldWebExField as object
    protected data oFormFieldWebExFieldSet as object
    protected data oFormFieldWebExFieldGroup as object
    protected data oFormFieldGroupWebExInputGroup as object

    public method New(cFormTitle as character,lFormButtonsFirst as logical) as object

    public method AddField(cFormFieldLabel as character,cFormFieldName as character,cFormFieldType as character,cFormFieldPlaceHolder as character) as object
    public method AddFieldToRow(oFormFieldsWebExRow,cFormFieldLabel as character,cFormFieldName as character,cFormFieldType as character,cFormFieldPlaceHolder as character) as object
    public method AddFieldGroupToRow(oFormFieldsWebExRow,cFormFieldLabel as character,cFormFieldName as character,cFormFieldType as character,cFormFieldPlaceHolder as character,nFieldGroupColSize as numeric) as object

    public method AddButton(oFormWebExButton as object) as object

    public method RenderHTML() as character

    public method SetFormTitle(cFormTitle as character) as object
    public method SetFormMethod(cFormMethod as character) as object
    public method SetFormAction(cFormAction as character) as object
    public method SetFieldGroupColSize(nFieldGroupColSize as numeric) as object

endclass

method New(cFormTitle,lFormButtonsFirst) class WebExForm
    paramtype 1 var cFormTitle as character optional default "WEBEXFORM"
    paramtype 2 var lFormButtonsFirst as logical optional default .F.
    ::cFormTitle:=cFormTitle
    ::cFormMethod:="get"
    ::cFormAction:="#"
    ::lFormButtonsFirst:=lFormButtonsFirst
    ::jFormFieldWebExFields:=JSONObject():New()
    ::jFormWebExButtonButtons:=JSONObject():New()
    ::nFieldGroupColSize:=4//Default
    _Super:New("form")
    ::AddClass("p-3")
    ::AddClass("border")
    ::AddClass("rounded")
    ::oFormFieldWebExFieldSet:=WebExFieldSet():New(::cFormTitle)
    ::SetFormTitle()
    ::SetFormMethod()
    ::SetFormAction()
return(self)

method AddField(cFormFieldLabel,cFormFieldName,cFormFieldType,cFormFieldPlaceHolder) class WebExForm
    paramtype 1 var cFormFieldLabel as character
    paramtype 2 var cFormFieldName as character
    paramtype 3 var cFormFieldType as character
    paramtype 4 var cFormFieldPlaceHolder as character
    ::oFormFieldWebExField:=WebExField():New(cFormFieldLabel,cFormFieldName,cFormFieldType,cFormFieldPlaceHolder)
    ::jFormFieldWebExFields[::oFormFieldWebExField:GetID()]:=::oFormFieldWebExField
return(self)

method AddFieldToRow(oFormFieldsWebExRow,cFormFieldLabel,cFormFieldName,cFormFieldType,cFormFieldPlaceHolder) class WebExForm

    paramtype 1 var oFormFieldsWebExRow as object class WEBEXROW
    paramtype 2 var cFormFieldLabel as character
    paramtype 3 var cFormFieldName as character
    paramtype 4 var cFormFieldType as character
    paramtype 5 var cFormFieldPlaceHolder as character

    ::oFormFieldsWebExRow:=oFormFieldsWebExRow

    ::oFormFieldWebExField:=WebExField():New(cFormFieldLabel,cFormFieldName,cFormFieldType,cFormFieldPlaceHolder)

    ::oFormFieldsWebExRow:AddChild(::oFormFieldWebExField)
    ::jFormFieldWebExFields[::oFormFieldsWebExRow:GetID()]:=::oFormFieldsWebExRow

return(self)

method AddFieldGroupToRow(oFormFieldsWebExRow,cFormFieldLabel,cFormFieldName,cFormFieldType,cFormFieldPlaceHolder,nFieldGroupColSize) class WebExForm

    paramtype 1 var oFormFieldsWebExRow as object class WebExRow
    paramtype 2 var cFormFieldLabel as character
    paramtype 3 var cFormFieldName as character
    paramtype 4 var cFormFieldType as character
    paramtype 5 var cFormFieldPlaceHolder as character
    paramtype 6 var nFieldGroupColSize as numeric optional default 4

    ::oFormFieldsWebExRow:=oFormFieldsWebExRow

    ::nFieldGroupColSize:=nFieldGroupColSize

    // Monta estrutura
    ::oFormFieldWebExFieldGroup:=WebExFieldGroup():New(cFormFieldLabel,cFormFieldName,cFormFieldType,cFormFieldPlaceHolder,/*cTitle*/,::nFieldGroupColSize)
    ::oFormFieldsWebExRow:AddChild(::oFormFieldWebExFieldGroup)

    // Registra no form
    ::jFormFieldWebExFields[::oFormFieldsWebExRow:GetID()]:=::oFormFieldsWebExRow

    //Obtem o Grupo
    ::oFormFieldGroupWebExInputGroup:=::oFormFieldWebExFieldGroup:GetMemberData("oFieldGroupWebExInputGroup")

return(self)

method AddButton(oFormWebExButton) class WebExForm
    paramtype 1 var oFormWebExButton as object class WebExButton
    ::oFormWebExButton:=oFormWebExButton
    ::jFormWebExButtonButtons[::oFormWebExButton:GetID()]:=::oFormWebExButton
return(self)

method RenderHTML() class WebExForm

    local aNames as array

    local nName as numeric
    local nNames as numeric

    local jChilds as json

    local oChild as object

    ::SetFormTitle()
    ::SetFormMethod()
    ::SetFormAction()

    jChilds:=if(::lFormButtonsFirst,::jFormWebExButtonButtons,::jFormFieldWebExFields)

    aNames:=jChilds:GetNames()
    nNames:=Len(aNames)
    for nName:=1 to nNames
        cFormFieldName:=aNames[nName]
        oChild:=jChilds[cFormFieldName]
        ::oFormFieldWebExFieldSet:AddChild(oChild)
    next nName
    FWFreeArray(@aNames)

    jChilds:=if(::lFormButtonsFirst,::jFormFieldWebExFields,::jFormWebExButtonButtons)
    aNames:=jChilds:GetNames()
    nNames:=Len(aNames)
    for nName:=1 to nNames
        cFormFieldName:=aNames[nName]
        oChild:=jChilds[cFormFieldName]
        ::oFormFieldWebExFieldSet:AddChild(oChild)
    next nName
    FWFreeArray(@aNames)

    ::AddChild(::oFormFieldWebExFieldSet)

return(_Super:RenderHTML())

method SetFormTitle(cFormTitle) class WebExForm
    paramtype 1 var cFormTitle as character optional default ::cFormTitle
    ::SetMemberData("cFormTitle",cFormTitle)
    ::oFormFieldWebExFieldSet:SetFieldSetLegend(::cFormTitle)
return(self)

method SetFormMethod(cFormMethod) class WebExForm
    paramtype 1 var cFormMethod as character optional default ::cFormMethod
    ::SetMemberData("cFormMethod",cFormMethod)
    ::SetAttr("method",::cFormMethod)
return(self)

method SetFormAction(cFormAction) class WebExForm
    paramtype 1 var cFormAction as character optional default ::cFormAction
    ::SetMemberData("cFormAction",AllTrim(cFormAction))
    ::SetAttr("action",::cFormAction)
return(self)

method SetFieldGroupColSize(nFieldGroupColSize) class WebExForm
    paramtype 1 var nFieldGroupColSize as numeric optional default ::nFieldGroupColSize
    ::SetMemberData("nFieldGroupColSize",nFieldGroupColSize)
    if (valType(::oFormFieldWebExFieldGroup)=="O")
        ::oFormFieldWebExFieldGroup:SetFieldGroupColSize(::nFieldGroupColSize)
    endif
return(self)
