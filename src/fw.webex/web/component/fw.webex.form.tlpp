/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

namespace FWWebEx

#include "fw.webex.th"

class WebExForm from WebExControl

    protected data cFormTitle as character
    protected data cFormMethod as character
    protected data cFormAction as character

    protected data jWebExFieldFormFields as json
    protected data jFormButtons as json

    protected data lFormButtonsFirst as logical

    protected data oWebExFieldSetFormField as object

    public method New(cFormTitle as character,lFormButtonsFirst as logical) as object
    public method SetMethod(cFormMethod as character) as object
    public method SetAction(cFormAction as character) as object
    public method AddField(cLabel as character,cName as character,cType as character,cPlaceholder as character) as object
    public method AddFieldToRow(oWebExRowFormFields,cLabel as character,cName as character,cType as character,cPlaceholder as character) as object
    public method AddGroupToRow(oWebExRowFormFields,cLabel as character,cName as character,cType as character,cPlaceholder as character,nColSize as numeric,/*@*/oWebExInputGroupFormFieldRef as object) as object
    public method AddButton(oWebExButtonForm as object) as object

    public method RenderHTML() as character

endclass

method New(cFormTitle,lFormButtonsFirst) class WebExForm
    paramtype 1 var cFormTitle as character
    paramtype 2 var lFormButtonsFirst as logical optional default .F.
    ::cFormTitle:=cFormTitle
    ::lFormButtonsFirst:=lFormButtonsFirst
    _Super:New("form")
    ::SetMethod("get")
    ::SetAction("#")
    ::SetAttr("class","p-3 border rounded")
    ::jWebExFieldFormFields:=JSONObject():New()
    ::jFormButtons:=JSONObject():New()
    ::oWebExFieldSetFormField:=WebExFieldSet():New(::cFormTitle)
return(self)

method SetMethod(cFormMethod) class WebExForm
    paramtype 1 var cFormMethod as character optional default ::cFormMethod
    ::cFormMethod:=Lower(AllTrim(cFormMethod))
    ::SetAttr("method",::cFormMethod)
return(self)

method SetAction(cFormAction) class WebExForm
    paramtype 1 var cFormAction as character optional default ::cFormAction
    ::cFormAction:=AllTrim(cFormAction)
    ::SetAttr("action",::cFormAction)
return(self)

method AddField(cLabel,cName,cType,cPlaceholder) class WebExForm
    local oWebExFieldFormField as object
    paramtype 1 var cLabel as character
    paramtype 2 var cName as character
    paramtype 3 var cType as character
    paramtype 4 var cPlaceholder as character
    oWebExFieldFormField:=WebExField():New(cLabel,cName,cType,cPlaceholder)
    ::jWebExFieldFormFields[oWebExFieldFormField:GetID()]:=oWebExFieldFormField
return(oWebExFieldFormField)

method AddFieldToRow(oWebExRowFormFields,cLabel,cName,cType,cPlaceholder) class WebExForm
    local oWebExFieldFormField as object
    paramtype 1 var oWebExRowFormFields as object class WEBEXROW
    paramtype 2 var cLabel as character
    paramtype 3 var cName as character
    paramtype 4 var cType as character
    paramtype 5 var cPlaceholder as character
    oWebExFieldFormField:=WebExField():New(cLabel,cName,cType,cPlaceholder)
    oWebExRowFormFields:AddChild(oWebExFieldFormField)
    ::jWebExFieldFormFields[oWebExRowFormFields:GetID()]:=oWebExRowFormFields
return(oWebExFieldFormField)

method AddGroupToRow(oWebExRowFormFields,cLabel,cName,cType,cPlaceholder,nColSize,/*@*/oWebExInputGroupFormFieldRef) class WebExForm

    local oWebExColFormField as object

    paramtype 1 var oWebExRowFormFields as object class WebExRow
    paramtype 2 var cLabel as character
    paramtype 3 var cName as character
    paramtype 4 var cType as character
    paramtype 5 var cPlaceholder as character
    paramtype 6 var nColSize as numeric optional
    paramtype 7 var oWebExInputGroupFormFieldRef as object optional

    // Monta estrutura
    oWebExColFormField:=WebExFieldGroup():New(cLabel,cName,cType,cPlaceholder,/*cTitle*/,nColSize)
    oWebExRowFormFields:AddChild(oWebExColFormField)

    // Registra no form
    ::jWebExFieldFormFields[oWebExRowFormFields:GetID()]:=oWebExRowFormFields

    //Retorna o Grupo por Referencia
    oWebExInputGroupFormFieldRef:=oWebExColFormField:GetObjWebExInputGroupInputFieldGroup()

return(oWebExColFormField)

method AddButton(oWebExButtonForm) class WebExForm
    paramtype 1 var oWebExButtonForm as object class WebExButton
    ::jFormButtons[oWebExButtonForm:GetID()]:=oWebExButtonForm
return(oWebExButtonForm)

method RenderHTML() class WebExForm

    local aNames as array

    local nName as numeric
    local nNames as numeric

    local jChilds as json

    local oChild as object

    ::SetMethod()
    ::SetAction()

    jChilds:=if(::lFormButtonsFirst,::jFormButtons,::jWebExFieldFormFields)

    aNames:=jChilds:GetNames()
    nNames:=Len(aNames)
    for nName:=1 to nNames
        cName:=aNames[nName]
        oChild:=jChilds[cName]
        ::oWebExFieldSetFormField:AddChild(oChild)
    next nName

    FWFreeArray(@aNames)

    jChilds:=if(::lFormButtonsFirst,::jWebExFieldFormFields,::jFormButtons)
    aNames:=jChilds:GetNames()
    nNames:=Len(aNames)
    for nName:=1 to nNames
        cName:=aNames[nName]
        oChild:=jChilds[cName]
        ::oWebExFieldSetFormField:AddChild(oChild)
    next nName

    ::AddChild(::oWebExFieldSetFormField)

return(_Super:RenderHTML())
