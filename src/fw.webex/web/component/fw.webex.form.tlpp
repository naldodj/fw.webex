/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

namespace FWWebEx

#include "fw.webex.th"

class WebExForm from WebExControl

    protected data cFormTitle as character
    protected data cFormMethod as character
    protected data cFormAction as character

    protected data jWebExFieldFormFields as json
    protected data jWebExButtonFormButtons as json

    protected data lFormButtonsFirst as logical

    protected data nWebExColFieldGroupColSize as numeric

    protected data oWebExButtonForm as object
    protected data oWebExRowFormFields as object
    protected data oWebExFieldFormField as object
    protected data oWebExFieldSetFormField as object
    protected data oWebExFieldGroupFormField as object
    protected data oWebExInputGroupInputFieldGroupFormField as object

    public method New(cFormTitle as character,lFormButtonsFirst as logical) as object

    public method AddField(cFormFieldLabel as character,cFormFieldName as character,cFormFieldType as character,cFormFieldPlaceHolder as character) as object
    public method AddFieldToRow(oWebExRowFormFields,cFormFieldLabel as character,cFormFieldName as character,cFormFieldType as character,cFormFieldPlaceHolder as character) as object
    public method AddFieldGroupToRow(oWebExRowFormFields,cFormFieldLabel as character,cFormFieldName as character,cFormFieldType as character,cFormFieldPlaceHolder as character,nWebExColFieldGroupColSize as numeric) as object

    public method AddButton(oWebExButtonForm as object) as object

    public method RenderHTML() as character

    public method SetFormTitle(cFormTitle as character) as object
    public method SetFormMethod(cFormMethod as character) as object
    public method SetFormAction(cFormAction as character) as object
    public method SetWebExColFieldGroupColSize(nWebExColFieldGroupColSize as numeric) as object

endclass

method New(cFormTitle,lFormButtonsFirst) class WebExForm
    paramtype 1 var cFormTitle as character optional default "WEBEXFORM"
    paramtype 2 var lFormButtonsFirst as logical optional default .F.
    ::cFormTitle:=cFormTitle
    ::cFormMethod:="get"
    ::cFormAction:="#"
    ::lFormButtonsFirst:=lFormButtonsFirst
    ::jWebExFieldFormFields:=JSONObject():New()
    ::jWebExButtonFormButtons:=JSONObject():New()
    ::nWebExColFieldGroupColSize:=4//Default
    _Super:New("form")
    ::AddClass("p-3")
    ::AddClass("border")
    ::AddClass("rounded")
    ::oWebExFieldSetFormField:=WebExFieldSet():New(::cFormTitle)
    ::SetFormTitle()
    ::SetFormMethod()
    ::SetFormAction()
return(self)

method AddField(cFormFieldLabel,cFormFieldName,cFormFieldType,cFormFieldPlaceHolder) class WebExForm
    paramtype 1 var cFormFieldLabel as character
    paramtype 2 var cFormFieldName as character
    paramtype 3 var cFormFieldType as character
    paramtype 4 var cFormFieldPlaceHolder as character
    ::oWebExFieldFormField:=WebExField():New(cFormFieldLabel,cFormFieldName,cFormFieldType,cFormFieldPlaceHolder)
    ::jWebExFieldFormFields[::oWebExFieldFormField:GetID()]:=::oWebExFieldFormField
return(self)

method AddFieldToRow(oWebExRowFormFields,cFormFieldLabel,cFormFieldName,cFormFieldType,cFormFieldPlaceHolder) class WebExForm

    paramtype 1 var oWebExRowFormFields as object class WEBEXROW
    paramtype 2 var cFormFieldLabel as character
    paramtype 3 var cFormFieldName as character
    paramtype 4 var cFormFieldType as character
    paramtype 5 var cFormFieldPlaceHolder as character

    ::oWebExRowFormFields:=oWebExRowFormFields

    ::oWebExFieldFormField:=WebExField():New(cFormFieldLabel,cFormFieldName,cFormFieldType,cFormFieldPlaceHolder)

    ::oWebExRowFormFields:AddChild(::oWebExFieldFormField)
    ::jWebExFieldFormFields[::oWebExRowFormFields:GetID()]:=::oWebExRowFormFields

return(self)

method AddFieldGroupToRow(oWebExRowFormFields,cFormFieldLabel,cFormFieldName,cFormFieldType,cFormFieldPlaceHolder,nWebExColFieldGroupColSize) class WebExForm

    paramtype 1 var oWebExRowFormFields as object class WebExRow
    paramtype 2 var cFormFieldLabel as character
    paramtype 3 var cFormFieldName as character
    paramtype 4 var cFormFieldType as character
    paramtype 5 var cFormFieldPlaceHolder as character
    paramtype 6 var nWebExColFieldGroupColSize as numeric optional default 4

    ::oWebExRowFormFields:=oWebExRowFormFields

    ::nWebExColFieldGroupColSize:=nWebExColFieldGroupColSize

    // Monta estrutura
    ::oWebExFieldGroupFormField:=WebExFieldGroup():New(cFormFieldLabel,cFormFieldName,cFormFieldType,cFormFieldPlaceHolder,/*cTitle*/,::nWebExColFieldGroupColSize)
    ::oWebExRowFormFields:AddChild(::oWebExFieldGroupFormField)

    // Registra no form
    ::jWebExFieldFormFields[::oWebExRowFormFields:GetID()]:=::oWebExRowFormFields

    //Obtem o Grupo
    ::oWebExInputGroupInputFieldGroupFormField:=::oWebExFieldGroupFormField:GetMemberData("oWebExInputGroupInputFieldGroup")

return(self)

method AddButton(oWebExButtonForm) class WebExForm
    paramtype 1 var oWebExButtonForm as object class WebExButton
    ::oWebExButtonForm:=oWebExButtonForm
    ::jWebExButtonFormButtons[::oWebExButtonForm:GetID()]:=::oWebExButtonForm
return(self)

method RenderHTML() class WebExForm

    local aNames as array

    local nName as numeric
    local nNames as numeric

    local jChilds as json

    local oChild as object

    ::SetFormTitle()
    ::SetFormMethod()
    ::SetFormAction()

    jChilds:=if(::lFormButtonsFirst,::jWebExButtonFormButtons,::jWebExFieldFormFields)

    aNames:=jChilds:GetNames()
    nNames:=Len(aNames)
    for nName:=1 to nNames
        cFormFieldName:=aNames[nName]
        oChild:=jChilds[cFormFieldName]
        ::oWebExFieldSetFormField:AddChild(oChild)
    next nName
    FWFreeArray(@aNames)

    jChilds:=if(::lFormButtonsFirst,::jWebExFieldFormFields,::jWebExButtonFormButtons)
    aNames:=jChilds:GetNames()
    nNames:=Len(aNames)
    for nName:=1 to nNames
        cFormFieldName:=aNames[nName]
        oChild:=jChilds[cFormFieldName]
        ::oWebExFieldSetFormField:AddChild(oChild)
    next nName
    FWFreeArray(@aNames)

    ::AddChild(::oWebExFieldSetFormField)

return(_Super:RenderHTML())

method SetFormTitle(cFormTitle) class WebExForm
    paramtype 1 var cFormTitle as character optional default ::cFormTitle
    ::SetMemberData("cFormTitle",cFormTitle)
    ::oWebExFieldSetFormField:SetFieldSetLegend(::cFormTitle)
return(self)

method SetFormMethod(cFormMethod) class WebExForm
    paramtype 1 var cFormMethod as character optional default ::cFormMethod
    ::SetMemberData("cFormMethod",cFormMethod)
    ::SetAttr("method",::cFormMethod)
return(self)

method SetFormAction(cFormAction) class WebExForm
    paramtype 1 var cFormAction as character optional default ::cFormAction
    ::SetMemberData("cFormAction",AllTrim(cFormAction))
    ::SetAttr("action",::cFormAction)
return(self)

method SetWebExColFieldGroupColSize(nWebExColFieldGroupColSize) class WebExForm
    paramtype 1 var nWebExColFieldGroupColSize as numeric optional default ::nWebExColFieldGroupColSize
    ::SetMemberData("nWebExColFieldGroupColSize",nWebExColFieldGroupColSize)
    if (valType(::oWebExFieldGroupFormField)=="O")
        ::oWebExFieldGroupFormField:SetWebExColFieldGroupColSize(::nWebExColFieldGroupColSize)
    endif
return(self)
