/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

namespace FWWebEx

#include "fw.webex.th"

class WebExCardKPI from WebExControl

    protected data cCardKPITitle as character
    protected data cCardKPIValue as character
    protected data cCardKPITheme as character

    protected data oCardKPIIcon as object
    protected data oCardKPIBody as object
    protected data oCardKPITitle as object
    protected data oCardKPIText as object
    protected data oCardKPIStrong as object

    public method New(cCardKPITitle as character,cCardKPIValue as character,cCardKPITheme as character,oCardKPIIcon as object) as object

    public method SetCardKPITitle(cCardKPITitle as character) as object
    public method SetCardKPIValue(cCardKPIValue as character) as object
    public method SetCardKPITheme(cCardKPITheme as character) as object

endclass

method New(cCardKPITitle,cCardKPIValue,cCardKPITheme,oCardKPIIcon) class WebExCardKPI

    local lHasIcon as logical

    paramtype 1 var cCardKPITitle as character optional default ""
    paramtype 2 var cCardKPIValue as character optional default ""
    paramtype 3 var cCardKPITheme as character optional default "bg-primary"
    paramtype 4 var oCardKPIIcon as object class WebExIcon optional

    ::cCardKPITitle:=cCardKPITitle
    ::cCardKPIValue:=cCardKPIValue
    ::cCardKPITheme:=Lower(AllTrim(cCardKPITheme))

    _Super:New("div")
    ::SetEnableIcons()

    ::AddClass("card")
    ::AddClass("mb-3")
    ::AddClass("border-secondary")
    ::SetCardKPITheme(::cCardKPITheme)

    ::oCardKPITitle:=WebExH5():New()
    ::oCardKPITitle:AddClass("card-title")

    ::oCardKPIIcon:=oCardKPIIcon
    lHasIcon:=(ValType(::oCardKPIIcon)=="O")
    if (lHasIcon)
        if (::oCardKPIIcon:GetMemberData("lIconBefore",.F.))
            if (!Empty(::oCardKPIIcon))
                ::oCardKPITitle:AddChild(::oCardKPIIcon)
            endif
        endif
    endif

    ::oCardKPIStrong:=WebExStrong():New(::cCardKPITitle)
    ::oCardKPITitle:AddChild(::oCardKPIStrong)

    if (lHasIcon)
        if (!::oCardKPIIcon:GetMemberData("lIconBefore",.F.))
            if (!Empty(::oCardKPIIcon))
                ::oCardKPITitle:AddChild(::oCardKPIIcon)
            endif
        endif
    endif

    ::oCardKPIText:=WebExParagraph():New()
    ::oCardKPIText:AddClass("card-text")
    ::SetCardKPIValue(::cCardKPIValue)

    ::oCardKPIBody:=WebExDiv():New()
    ::oCardKPIBody:AddClass("card-body")
    ::oCardKPIBody:AddChild(::oCardKPITitle)
    ::oCardKPIBody:AddChild(::oCardKPIText)

    ::AddChild(::oCardKPIBody)

return(self)

method SetCardKPITitle(cCardKPITitle) class WebExCardKPI
    paramtype 1 var cCardKPITitle as character optional default ::cCardKPITitle
    ::SetMemberData("cCardKPITitle",cCardKPITitle)
    ::oCardKPIStrong:SetStrongTextContent(::cCardKPITitle)
return(self)

method SetCardKPIValue(cCardKPIValue) class WebExCardKPI
    paramtype 1 var cCardKPIValue as character optional default ::cCardKPIValue
    ::SetMemberData("cCardKPIValue",cCardKPIValue)
    ::oCardKPIText:SetContent(::cCardKPIValue)
return(self)

method SetCardKPITheme(cCardKPITheme) class WebExCardKPI
    paramtype 1 var cCardKPITheme as character optional default ::cCardKPITheme
    ::SetMemberData("cCardKPITheme",cCardKPITheme)
    if (!Empty(::cCardKPITheme))
        ::AddClass(::cCardKPITheme)
    endif
return(self)
