/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

namespace FWWebEx

#include "fw.webex.th"

class WebExNavSide from WebExControl

    public data cNavSideBrand as character

    public data jNavSideItems as json

    public method New(cNavSideBrand as character) as object
    public method SetBrand(cNavSideBrand as character) as object
    public method AddItem(cNavSideLabel as character,cNavSideHRef as character,oNavIcon as object) as object
    public method AddDivider() as object
    public method AddHeader(cNavSideHeaderText as character) as object
    public method RenderHTML() as character

endclass

method New(cNavSideBrand) class WebExNavSide
    paramtype 1 var cNavSideBrand as character optional default ""
    ::jNavSideItems:=JSONObject():New()
    ::cNavSideBrand:=cNavSideBrand
    _Super:New("nav")
    ::SetAttr("class","sidebar bg-body border-end")
    ::SetAttr("id",::GetFixedID())
return(self)

method SetBrand(cNavSideBrand) class WebExNavSide
    ::cNavSideBrand:=cNavSideBrand
return(self)

method AddItem(cNavSideLabel,cNavSideHRef,oNavIcon) class WebExNavSide

    local oItem as object
    local oLink as object
    local oSpan as object

    paramtype 1 var cNavSideLabel as character
    paramtype 2 var cNavSideHRef as character
    paramtype 3 var oNavIcon as object class WEBEXICON optional

    oSpan:=WebExControl():New("span")
    oSpan:SetContent(cNavSideLabel)

    oLink:=WebExControl():New("a")
    oLink:SetAttr("class","nav-link")
    oLink:SetAttr("href",cNavSideHRef)
    if (oNavIcon!=nil)
        oLink:AddChild(oNavIcon)
    endif
    oLink:AddChild(oSpan)

    oItem:=WebExControl():New("li")
    oItem:SetAttr("class","nav-item")
    oItem:AddChild(oLink)

    ::jNavSideItems[oItem:GetID()]:=oItem

return(oItem)

method AddDivider() class WebExNavSide
    local oDivider:=WebExControl():New("hr")
    oDivider:SetAttr("class","my-2")
    ::jNavSideItems[oDivider:GetID()]:=oDivider
return(oDivider)

method AddHeader(cNavSideHeaderText) class WebExNavSide
    local oHeader as object
    paramtype 1 var cNavSideHeaderText as character
    oHeader:=WebExControl():New("div")
    oHeader:SetAttr("class","text-muted text-uppercase px-3 small fw-bold")
    oHeader:SetContent(cNavSideHeaderText)
    ::jNavSideItems[oHeader:GetID()]:=oHeader
return(oHeader)

method RenderHTML() class WebExNavSide

    local cHTML as character

    local oList as object
    local oBrand as object
    local oWrapper as object

    oWrapper:=WebExControl():New("div")
    oWrapper:SetAttr("class","d-flex flex-column flex-shrink-0 p-3")
    //oWrapper:SetAttr("style","min-height: 100vh")

    if (!Empty(::cNavSideBrand))
        oBrand:=WebExControl():New("a")
        oBrand:SetAttr("href","#")
        oBrand:SetAttr("class","navbar-brand mb-3")
        oBrand:SetContent(::cNavSideBrand)
        oWrapper:AddChild(oBrand)
    endif

    oList:=WebExControl():New("ul")
    oList:SetAttr("class","nav nav-pills flex-column mb-auto")
    aEval(::jNavSideItems:GetNames(),{|k|oList:AddChild(::jNavSideItems[k])})
    oWrapper:AddChild(oList)

    ::AddChild(oWrapper)

    cHTML:=_Super:RenderHTML()

return(cHTML)
