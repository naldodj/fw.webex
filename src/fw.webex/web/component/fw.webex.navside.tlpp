/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

namespace FWWebEx

#include "fw.webex.th"

class WebExNavSide from WebExControl

    protected data cNavSideBrand as character

    protected data oNavSideItem as object
    protected data oNavSideLink as object
    protected data oNavSideSpan as object

    protected data oNavSideList as object
    protected data oNavSideBrand as object
    protected data oNavSideWrapper as object

    public method New(cNavSideBrand as character) as object

    public method AddItem(cNavSideLabel as character,cNavSideHRef as character,oNavIcon as object) as object
    public method AddDivider() as object
    public method AddHeader(cNavSideHeaderText as character) as object
    public method RenderHTML() as character

    public method SetNavSideBrand(cNavSideBrand as character) as object

endclass

method New(cNavSideBrand) class WebExNavSide
    paramtype 1 var cNavSideBrand as character optional default ""
    ::cNavSideBrand:=cNavSideBrand
    ::oNavSideList:=WebExUL():New()
    ::oNavSideList:AddClass("nav nav-pills flex-column mb-auto")
    ::oNavSideWrapper:=WebExDiv():New()
    ::oNavSideWrapper:AddClass("d-flex flex-column flex-shrink-0 p-3")
    _Super:New("nav")
    ::AddClass("sidebar bg-body border-end")
    ::SetAttr("id",::GetFixedID())
    ::SetNavSideBrand()
return(self)

method AddItem(cNavSideLabel,cNavSideHRef,oNavIcon) class WebExNavSide

    paramtype 1 var cNavSideLabel as character
    paramtype 2 var cNavSideHRef as character
    paramtype 3 var oNavIcon as object class WEBEXICON optional

    ::oNavSideSpan:=WebExSpan():New()
    ::oNavSideSpan:SetContent(cNavSideLabel)

    ::oNavSideLink:=WebExAnchor():New()
    ::oNavSideLink:AddClass("nav-link")
    ::oNavSideLink:SetAttr("href",cNavSideHRef)
    if (oNavIcon!=nil)
        ::oNavSideLink:AddChild(oNavIcon)
    endif
    ::oNavSideLink:AddChild(::oNavSideSpan)

    ::oNavSideItem:=WebExLI():New()
    ::oNavSideItem:AddClass("nav-item")
    ::oNavSideItem:AddChild(::oNavSideLink)
    ::oNavSideList:AddChild(::oNavSideItem)

return(::oNavSideItem)

method AddDivider() class WebExNavSide
    local oDivider:=WebExHR():New()
    oDivider:DelClassRegex("my-[0-9]+") // remove qualquer my-* anterior (se tiver)
    oDivider:AddClass("my-2")
    ::oNavSideList:AddChild(oDivider)
return(oDivider)

method AddHeader(cNavSideHeaderText) class WebExNavSide
    local oHeader as object
    paramtype 1 var cNavSideHeaderText as character
    oHeader:=WebExDiv():New()
    oHeader:AddClass("text-muted text-uppercase px-3 small fw-bold")
    oHeader:SetContent(cNavSideHeaderText)
    ::oNavSideList:AddChild(oHeader)
return(oHeader)

method RenderHTML() class WebExNavSide

    local cHTML as character

    if (!Empty(::cNavSideBrand))
        ::oNavSideWrapper:AddChild(::oNavSideBrand)
    endif

    ::oNavSideWrapper:AddChild(::oNavSideList)

    ::AddChild(::oNavSideWrapper)

    cHTML:=_Super:RenderHTML()

return(cHTML)

method SetNavSideBrand(cNavSideBrand) class WebExNavSide
    paramtype 1 var cNavSideBrand as character optional default ::cNavSideBrand
    ::cNavSideBrand:=cNavSideBrand
    if (!Empty(::cNavSideBrand))
        if (valType(::oNavSideBrand)!="O")
            ::oNavSideBrand:=WebExAnchor():New()
            ::oNavSideBrand:SetAttr("href","#")
            ::oNavSideBrand:AddClass("navbar-brand mb-3")
        endif
        ::oNavSideBrand:SetContent(::cNavSideBrand)
    endif
return(self)
