/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

namespace FWWebEx

#include "fw.webex.th"

class WebExBody from WebExControl
    public method New() as object
    public method AddChild(oBodyChild as object) as object
end class

method New() class WebExBody

    local cTableStyle as character

    _Super:New("body")

    ::AddClass("p-4")
    ::AddClass("d-flex")
    ::AddClass("flex-column")
    ::AddClass("min-vh-100")

    WITH WEBEXOBJECT self
        WITH WEBEXOBJECT CLASS WebExDiv
            .:SetAttr("style", "position: fixed; top: 0; left: 0; width: 100%; z-index: 9999; background-color: #212529;")
            // Barra de status de conexao
            WITH WEBEXOBJECT CLASS WebExDiv
                .:SetFixedID("fwwebex-connection-status")
                .:SetAttr("style", "display: none; color: #d4a437; font-weight: 500; font-size: 0.8rem; padding: 0.5rem 0.75rem; text-align: center;")
                .:SetContent("Tentando conectar ao Protheus")
                // Pinguinhos animados
                WITH WEBEXOBJECT CLASS WebExSpan
                    .:SetFixedID("fwwebex_connection_status_dot1")
                    .:SetContent(".")
                END WEBEXOBJECT
                WITH WEBEXOBJECT CLASS WebExSpan
                    .:SetFixedID("fwwebex_connection_status_dot2")
                    .:SetContent(".")
                END WEBEXOBJECT
                WITH WEBEXOBJECT CLASS WebExSpan
                    .:SetFixedID("fwwebex_connection_status_dot3")
                    .:SetContent(".")
                END WEBEXOBJECT
            END WEBEXOBJECT
            // Loader para DataTables
            WITH WEBEXOBJECT CLASS WebExControl TYPE div
                .:SetAttr("id","fwwebex-datatable-custom-loader")
                .:SetAttr("style", "display: none; width: 100%; padding: 1rem;")
                WITH WEBEXOBJECT CLASS WebExControl TYPE div
                    .:AddClass("progress")
                    WITH WEBEXOBJECT CLASS WebExControl TYPE div
                        .:AddClass("progress-bar")
                        .:AddClass("progress-bar-striped")
                        .:AddClass("progress-bar-animated")
                        .:AddClass("bg-primary")
                        .:SetAttr("style","width: 100%")
                        .:SetContent("Carregando...")
                    END WEBEXOBJECT
                END WEBEXOBJECT
            END WEBEXOBJECT
        END WEBEXOBJECT
        WITH WEBEXOBJECT CLASS WebExControl TYPE style
            beginContent var cTableStyle
                table.dataTable.compact tbody td {
                    padding: 4px 8px !important;
                }
                #fwwebex-datatable-custom-loader {
                    position: absolute !important;
                    top: 0 !important;
                    left: 0 !important;
                    right: 0 !important;
                    height: 4px !important;
                    background: linear-gradient(270deg, #0dcaf0, #0d6efd) !important;
                    animation: progressbar-stripes 1s linear infinite !important;
                }
                @keyframes progressbar-stripes {
                0% { background-position: 1rem 0; }
                100% { background-position: 0 0; }
                }
            endContent
        END WEBEXOBJECT
    END WEBEXOBJECT NOADDCHILD

return(self)

method AddChild(oBodyChild) class WebExBody
    local cClassName as character
    paramtype 1 var oBodyChild as object
    cClassName:=GetClassName(oBodyChild)
    if (!Left(cClassName,8)=="FWWEBEX.")
        CLASSPARAMEXCEPTION PARAM 1 VAR oBodyChild TEXT cClassName MESSAGE "Object must belong to the FWWEBEX namespace. Got '"+cClassName+"' instead. Check your class declaration."
    elseif ((!MethIsMemberOf(oBodyChild,"ISKINDOF",.T.)).or.(!oBodyChild:IsKindOf("WEBEXCONTROL",.T.)))
        CLASSPARAMEXCEPTION PARAM 1 VAR oBodyChild TEXT cClassName MESSAGE "Object must inherit from FWWEBEX.WebExControl. Received: "+cClassName+"."
    elseif (oBodyChild:IsKindOf("WEBEXBODY",.T.))
        CLASSPARAMEXCEPTION PARAM 1 VAR oBodyChild TEXT cClassName MESSAGE "Cannot nest a <body> element inside another <body>. Received: "+cClassName+". A WebExBody must be the root or direct child of WebExPage/WebExPage."
    endif
    _Super:AddChild(oBodyChild)
return(self)
