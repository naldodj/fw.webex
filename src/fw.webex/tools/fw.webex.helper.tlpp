/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

namespace FWWebEx

#include "fw.webex.th"

static __cCHR10:=CHR(10) as character
static __cCHR13:=CHR(13) as character

static __jEmojis as json

class WebExHelper

    static method EscapeAll(cValue as character) as character

    static method EscapeHTMLScript(cHTML as character) as character
    static method EscapeJavaScript(cCode as character) as character
    static method EscapeHTMLAttribute(cValue as character) as character

    static method ConvertHeaderToJSObject(cHeaderString as character) as character

    static method GetEmojiHtmlJSON() as json
    static method GetEmojiHtmlCode(cName as character) as character

    static method StrReplace(cTemplate as character,vSubstitutions as variant) as character

end class

static method EscapeAll(cValue) class WebExHelper
    paramtype 1 var cValue as character
    cValue:=WebExHelper():EscapeHTMLScript(cValue)
    cValue:=WebExHelper():EscapeHTMLAttribute(cValue)
    cValue:=WebExHelper():EscapeJavaScript(cValue)
return(cValue)

static method EscapeHTMLScript(cHTML) class WebExHelper
    paramtype 1 var cHTML as character
    cHTML:=AllTrim(cHTML)
    cHTML:=WebExHelper():StrReplace(;
        cHTML;
       ,{;
            {"'","\\'"};
           ,{__cCHR13,""};
           ,{__cCHR10,""};
       };
    )
return(cHTML)

static method EscapeJavaScript(cCode) class WebExHelper
    paramtype 1 var cCode as character
    cCode:=WebExHelper():StrReplace(;
        cCode;
       ,{;
            {"\\","\\\\"};
           ,{"'","\\'"};
           ,{'"','\\\"'};
           ,{__cCHR13,""};
           ,{__cCHR10,""};
       };
    )
return(cCode)

static method EscapeHTMLAttribute(cValue) class WebExHelper
    paramtype 1 var cValue as character
    cValue:=WebExHelper():StrReplace(;
        cValue;
       ,{;
            {'"',"&quot;"};
           ,{"'","&#39;"};
       };
    )
return(cValue)

static method ConvertHeaderToJSObject(cHeaderString) class WebExHelper

    local aHeaderString as array

    paramtype 1 var cHeaderString as character

    aHeaderString:=StrTokArr2(cHeaderString,":")
    if (Len(aHeaderString)==2)
        cHeaderString:="'"+AllTrim(aHeaderString[1])+"'"
        cHeaderString+=":"
        cHeaderString+="'"+AllTrim(aHeaderString[2])+"'"
    endif

return(cHeaderString)

static method GetEmojiHtmlJSON() class WebExHelper

    local jEmojis as json

    if (__jEmojis==nil)
        __jEmojis:={;
             "alarm_clock"      : "&#9200;";
            ,"aquarius"         : "&#9810;";
            ,"aries"            : "&#9800;";
            ,"cancer"           : "&#9803;";
            ,"calendar"         : "&#128197;";
            ,"capricorn"        : "&#9809;";
            ,"check"            : "&#10004;";
            ,"clap"             : "&#128079;";
            ,"clock"            : "&#128336;";
            ,"clock_3"          : "&#128339;";
            ,"clover"           : "&#127808;";
            ,"cross"            : "&#10060;";
            ,"crystal_ball"     : "&#128302;";
            ,"date"             : "&#128197;";
            ,"earth"            : "&#127757;";
            ,"eyes"             : "&#128064;";
            ,"fire"             : "&#128293;";
            ,"gemini"           : "&#9802;";
            ,"glowing_star"     : "&#127775;";
            ,"heart"            : "&#10084;";
            ,"heart_with_arrow" : "&#128152;";
            ,"hourglass"        : "&#8987;";
            ,"info"             : "&#8505;";
            ,"leo"              : "&#9804;";
            ,"libra"            : "&#9806;";
            ,"lotus"            : "&#129763;";
            ,"musical_note"     : "&#127925;";
            ,"pisces"           : "&#9811;";
            ,"rainbow"          : "&#127752;";
            ,"rocket"           : "&#128640;";
            ,"sagittarius"      : "&#9808;";
            ,"scorpio"          : "&#9807;";
            ,"smile"            : "&#128522;";
            ,"star"             : "&#11088;";
            ,"star_circle"      : "&#55368;&#56873;";
            ,"sweat_drops"      : "&#128166;";
            ,"taurus"           : "&#9801;";
            ,"thinking"         : "&#129300;";
            ,"thumbs_down"      : "&#128078;";
            ,"thumbs_up"        : "&#128077;";
            ,"tibetan_flower"   : "&#55141;";
            ,"virgo"            : "&#9805;";
            ,"warning"          : "&#9888;";
        }
    endif

    jEmojis:=JSONObject():New()
    jEmojis:FromJSON(__jEmojis:ToJSON())

return(jEmojis)

static method GetEmojiHtmlCode(cName) class WebExHelper

    local cEmojiHtmlCode as character

    local jEmojis:=WebExHelper():GetEmojiHtmlJSON() as json

    paramtype 1 var cName as character

    cName:=Lower(AllTrim(cName))
    if (jEmojis:HasProperty(cName))
        cEmojiHtmlCode:=jEmojis[cName]
    else
        cEmojiHtmlCode:=""
    endif

    FreeObj(@jEmojis)

return(cEmojiHtmlCode)

method StrReplace(cTemplate,vSubstitutions) class WebExHelper

    local aNames as array
    local cName as character

    local cApplied as character

    local i as numeric

    paramtype 1 var cTemplate as character
    paramtype 2 var vSubstitutions as array,json

    cApplied:=cTemplate

    if (ValType(vSubstitutions)=="J")
        aNames:=vSubstitutions:GetNames()
        for i:=1 to Len(aNames)
            cName:=aNames[i]
            cApplied:=StrTran(cApplied,cName,vSubstitutions[cName])
        next i
    else
        aNames:=vSubstitutions
        for i:=1 to Len(vSubstitutions)
            if (Len(aNames[i])!=2)
                loop
            endif
            cName:=aNames[i][1]
            cApplied:=StrTran(cApplied,cName,aNames[i][2])
        next i
    endif

return(cApplied)
