/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

+---> WebExPage ("Exemplo 005 - Formulario ViaCEP")
      |
      +---> WebExForm ("Consulta de CEP")
            |
            +---> input[name="cep"] (text)           <- Digite o CEP
            +---> input[name="logradouro"] (text)    <- preenchido via JS
            +---> input[name="bairro"] (text)
            +---> input[name="localidade"] (text)
            +---> input[name="uf"] (text)
            +---> input[name="ibge"] (text)
            |
            +---> WebExControl (div#resultadoCEP)     <- onde aparece feedback da busca
            |
            +---> WebExButton ("Buscar")
                  |
                  +---> onclick="buscarCEP(); return false;"
      |
      +---> WebExScript
            |
            +---> <script>
                  |
                  +---> funcao JS buscarCEP()
                          |
                          +--->  faz fetch para ViaCEP
                          |
                          +--->  preenche campos e div#resultadoCEP dinamicamente

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

#include "fw.webex.th"

using namespace FWWebEx

procedure u_FWWebExExample_017()
    local bExecute as codeblock
    local cHTML as character
    local cHTMLFile as character
    local cProcName:=ProcName() as character
    local oTWebChannel:=WebApp():GetTWebChannel() as object
    if (!oTWebChannel:lConnected)
        __oTWebChannel:Connect()
    endif
    bExecute:={||FWMsgRun(nil,{||cHTMLFile:=FWWebExExample_017(@cHTML)},"Aguarde",cProcName)}
    FWExampleTools():Execute(bExecute,cProcName,.T.,.T.)
    if (!Empty(cHTMLFile).and.File(cHTMLFile))
        FWExampleTools():htmlFileShow(cHTML,cProcName,cHTMLFile,oTWebChannel)
        fErase(cHTMLFile)
    endif
    WebFileTools():ObliterateFWWebExTmpFiles(.T.)
return

static function FWWebExExample_017(cHTML as character) as character

    local cScript as character
    local cProcName:=ProcName() as character
    local cHTMLFile:=cProcName as character

    local oDiv as object
    local oGroupRef as object

    local oFWWebExRow1 as object
    local oFWWebExRow2 as object
    local oFWWebExPage as object
    local oFWWebExMain as object
    local oFWWebExBody as object
    local oFWWebExForm as object
    local oFWWebExButton as object
    local oFWWebExScript as object

    //https://getbootstrap.com/docs/5.0/forms/form-control/
    oFWWebExForm:=WebExForm():New("Consulta de CEP")
    oFWWebExForm:DelAttr("class"):AddClass("row g-3")
    oFWWebExForm:SetMethod("get")
    oFWWebExForm:SetAction("#")

    // Campo de busca
    oFWWebExRow1:=WebExRow():New()
    oFWWebExForm:AddGroupToRow(oFWWebExRow1,"CEP","cep","text","Digite o CEP",4,@oGroupRef)

    //Carrega o Botao para Buscar os Dados ao Grupo
    oFWWebExButton:=WebExButton():New("Buscar")
    oFWWebExButton:SetAttr("onclick","buscarCEP(); return false;")
    oGroupRef:AddChild(oFWWebExButton)

    //Adiciona um Separador Horizontal
    oFWWebExRow1:AddChild(WebExHR():New())

    // Campos que serao atualizados via Ajax (ja definidos)
    oFWWebExRow2:=WebExRow():New()
    oFWWebExForm:AddFieldToRow(@oFWWebExRow2,"Logradouro","logradouro","text",""):AddClass("mb-3 col-md-4")
    oFWWebExForm:AddFieldToRow(@oFWWebExRow2,"Bairro","bairro","text",""):AddClass("mb-3 col-md-4")
    oFWWebExForm:AddFieldToRow(@oFWWebExRow2,"Cidade","localidade","text",""):AddClass("mb-3 col-md-4")
    oFWWebExForm:AddFieldToRow(@oFWWebExRow2,"UF","uf","text",""):DelAttr("class"):AddClass("mb-3 col-md-4")
    oFWWebExForm:AddFieldToRow(@oFWWebExRow2,"C&oacute;digo IBGE","ibge","text",""):AddClass("mb-3 col-md-4")

    oDiv:=WebExControl():New("div")
    oDiv:SetAttr("id","resultadoCEP")
    oFWWebExForm:AddChild(oDiv)

    // Adiciona o script para consulta Ajax do ViaCEP
    beginContent var cScript
        function buscarCEP() {
            const cep = document.getElementsByName('cep')[0].value.replace(/[^0-9]/g,'');
            if (cep.length !== 8) {
                document.getElementById('resultadoCEP').innerHTML = '<div class=\"alert alert-danger\">CEP inv&aacute;lido.</div>';
                return;
            }
            fetch('https://viacep.com.br/ws/' + cep + '/json/')
                .then(response => response.json())
                .then(data => {
                if (data.erro) {
                    document.getElementById('resultadoCEP').innerHTML = '<div class=\"alert alert-danger\">CEP n&atilde;o encontrado.</div>';
                } else {
                    document.getElementsByName('logradouro')[0].value = data.logradouro || '';
                    document.getElementsByName('bairro')[0].value = data.bairro || '';
                    document.getElementsByName('localidade')[0].value = data.localidade || '';
                    document.getElementsByName('uf')[0].value = data.uf || '';
                    document.getElementsByName('ibge')[0].value = data.ibge || '';
                    document.getElementById('resultadoCEP').innerHTML = '';
                }
            }).catch(() => {
                document.getElementById('resultadoCEP').innerHTML = '<div class=\"alert alert-danger\">Erro ao consultar o CEP.</div>';
            });
        }
    endContent

    oFWWebExScript:=WebExScript():New()
    oFWWebExScript:SetContent(cScript)

    oFWWebExMain:=WebExMain():New()
    oFWWebExMain:AddChild(oFWWebExForm)

    oFWWebExBody:=WebExBody():New()
    oFWWebExBody:AddChild(oFWWebExMain)

    oFWWebExPage:=WebExPage():New("Exemplo 005 - Formul&aacute;rio ViaCEP")
    oFWWebExPage:AddChild(oFWWebExBody)

    WebFileTools():HTMLFromControl(oFWWebExPage,oFWWebExPage:GetFWWebExTmpPath(),@cHTMLFile,@cHTML,.T.)

    oFWWebExPage:Clean()

    FreeObj(@oDiv)
    FreeObj(@oGroupRef)

    FreeObj(@oFWWebExRow1)
    FreeObj(@oFWWebExRow2)
    FreeObj(@oFWWebExPage)
    FreeObj(@oFWWebExMain)
    FreeObj(@oFWWebExBody)
    FreeObj(@oFWWebExForm)
    FreeObj(@oFWWebExButton)
    FreeObj(@oFWWebExScript)

return(cHTMLFile)
