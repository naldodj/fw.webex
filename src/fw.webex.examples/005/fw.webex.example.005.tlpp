/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

+---> WebExPage ("Exemplo 005 - Formulario ViaCEP")
      |
      +---> WebExForm ("Consulta de CEP")
            |
            +---> input[name="cep"] (text)           <- Digite o CEP
            +---> input[name="logradouro"] (text)    <- preenchido via JS
            +---> input[name="bairro"] (text)
            +---> input[name="localidade"] (text)
            +---> input[name="uf"] (text)
            +---> input[name="ibge"] (text)
            |
            +---> WebExControl (div#resultadoCEP)     <- onde aparece feedback da busca
            |
            +---> WebExButton ("Buscar")
                  |
                  +---> onclick="buscarCEP(); return false;"
      |
      +---> WebExScript
            |
            +---> <script>
                  |
                  +---> funcao JS buscarCEP()
                          |
                          +--->  faz fetch para ViaCEP
                          |
                          +--->  preenche campos e div#resultadoCEP dinamicamente

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

#include "fw.webex.th"

using namespace FWWebEx

procedure u_FWWebExExample_005()
    local bExecute as codeblock
    local cHTML as character
    local cHTMLFile as character
    local cProcName:=ProcName() as character
    bExecute:={||FWMsgRun(nil,{||cHTMLFile:=FWWebExExample_005(@cHTML)},"Aguarde",cProcName)}
    FWExampleTools():Execute(bExecute,cProcName,.T.)
    if (!Empty(cHTMLFile).and.File(cHTMLFile))
        FWExampleTools():htmlFileShow(cHTML,cProcName,cHTMLFile)
        fErase(cHTMLFile)
    endif
return

static function FWWebExExample_005(cHTML as character) as character

    local cScript as character
    local cProcName:=ProcName() as character
    local cHTMLFile:=cProcName as character

    local oDiv as object

    local oFWWebExPage as object
    local oFWWebExMain as object
    local oFWWebExBody as object
    local oFWWebExForm as object
    local oFWWebExButton as object
    local oFWWebExScript as object

    oFWWebExForm:=WebExForm():New("Consulta de CEP")
    oFWWebExForm:SetMethod("get")
    oFWWebExForm:SetAction("#")

    // Campo de busca
    oFWWebExForm:AddField("CEP","cep","text","Digite o CEP")

    // Campos que serao atualizados via Ajax (ja definidos)
    oFWWebExForm:AddField("Logradouro","logradouro","text","")
    oFWWebExForm:AddField("Bairro","bairro","text","")
    oFWWebExForm:AddField("Cidade","localidade","text","")
    oFWWebExForm:AddField("UF","uf","text","")
    oFWWebExForm:AddField("C&oacute;digo IBGE","ibge","text","")

    oDiv:=WebExControl():New("div")
    oDiv:SetAttr("id","resultadoCEP")

    oFWWebExButton:=WebExButton():New("Buscar")
    oFWWebExButton:SetAttr("onclick","buscarCEP(); return false;")

    oFWWebExForm:AddChild(oDiv)
    oFWWebExForm:AddChild(oFWWebExButton)

    // Adiciona o script para consulta Ajax do ViaCEP
    beginContent var cScript
        function buscarCEP() {
            const cep = document.getElementsByName('cep')[0].value.replace(/[^0-9]/g,'');
            if (cep.length !== 8) {
                document.getElementById('resultadoCEP').innerHTML = '<div class=\"alert alert-danger\">CEP inv&aacute;lido.</div>';
                return;
            }
            fetch('https://viacep.com.br/ws/' + cep + '/json/')
                .then(response => response.json())
                .then(data => {
                if (data.erro) {
                    document.getElementById('resultadoCEP').innerHTML = '<div class=\"alert alert-danger\">CEP n&atilde;o encontrado.</div>';
                } else {
                    document.getElementsByName('logradouro')[0].value = data.logradouro || '';
                    document.getElementsByName('bairro')[0].value = data.bairro || '';
                    document.getElementsByName('localidade')[0].value = data.localidade || '';
                    document.getElementsByName('uf')[0].value = data.uf || '';
                    document.getElementsByName('ibge')[0].value = data.ibge || '';
                    document.getElementById('resultadoCEP').innerHTML = '';
                }
            }).catch(() => {
                document.getElementById('resultadoCEP').innerHTML = '<div class=\"alert alert-danger\">Erro ao consultar o CEP.</div>';
            });
        }
    endContent

    oFWWebExScript:=WebExScript():New()
    oFWWebExScript:SetContent(cScript)

    oFWWebExMain:=WebExMain():New()
    oFWWebExMain:AddChild(oFWWebExForm)

    oFWWebExBody:=WebExBody():New()
    oFWWebExBody:AddChild(oFWWebExMain)

    oFWWebExPage:=WebExPage():New("Exemplo 005 - Formul&aacute;rio ViaCEP")
    oFWWebExPage:AddChild(oFWWebExBody)

    WebFileTools():HTMLFromControl(oFWWebExPage,oFWWebExPage:GetFWWebExTmpPath(),@cHTMLFile,@cHTML,.T.)

    oFWWebExPage:Clean()

    FreeObj(@oDiv)
    FreeObj(@oFWWebExPage)
    FreeObj(@oFWWebExMain)
    FreeObj(@oFWWebExBody)
    FreeObj(@oFWWebExForm)
    FreeObj(@oFWWebExScript)
    FreeObj(@oFWWebExButton)

return(cHTMLFile)
