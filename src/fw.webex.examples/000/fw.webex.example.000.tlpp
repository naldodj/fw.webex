/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

#include "fw.webex.th"

using namespace FWWebEx

/*
    Obs.:   Para chamada atraves do Menu do Protheus inclua FWWeb000 como funcao de usuario
            Em 01/07/2025 as 15:21:10 a equipe da TOTVS ainda nao resolveu a questao de funcoes TLPP com Nomes Longos no MENU.
            Eles poderiam colocar: tlpp.call(<cFunction>,[xParms]) para executar funcoes TL++ em Advpl nas chamadas do MENU.
            Fazer o Q. Bora criar um Wrapper chamado u_FWWeb000.
*/
procedure u_FWWeb000()
    /*
        Assim deveria ser a chamada no MENU:
        tlpp.call("u_FWWebExExample_000")
        Problemas: Quando Erro a Stack nao vem completa.
    */
    u_FWWebExExample_000()
return

procedure u_FWWebExExample_000()
    private cCadastro:="Exemplos FWWebEx"
    FWExampleTools():Execute({||FWWebExExample_000()},cCadastro,.T.)
return

static procedure FWWebExExample_000()

    local cRight as character
    local cExample:="" as character

    local jExamples:=JSONObject():New() as json

    local nTop as numeric
    local nLeft as numeric
    local nBottom as numeric
    local nRight as numeric

    local nExample as numeric
    local nExamples:=16 as numeric

    local oTDlg as object
    local oTMenu as object
    local oTPanel as object
    local oTBitMap as object
    local oFWDefSize as object
    local oWNDDefault as object

    jExamples["Forms"]:=JSONObject():New()
    jExamples["Tables"]:=JSONObject():New()
    jExamples["UIComponents"]:=JSONObject():New()
    jExamples["DashBoard"]:=JSONObject():New()
    jExamples["Nav"]:=JSONObject():New()
    jExamples["DumpDOM"]:=JSONObject():New()
    jExamples["iFrame"]:=JSONObject():New()

    for nExample:=1 to nExamples
        cExample:="u_FWWebExExample_"+StrZero(nExample,3)
        if (!FindFunction(cExample))
            Loop
        endif
        cRight:=Right(cExample,3)
        do case
        case (cRight$"003|005")
            jExamples["Forms"][cExample]:=&("{||"+cExample+"()}")
        case (cRight$"001|002|006|007|008|009")
            jExamples["Tables"][cExample]:=&("{||"+cExample+"()}")
        case (cRight$"004")
            jExamples["UIComponents"][cExample]:=&("{||"+cExample+"()}")
        case (cRight$"010|011")
            jExamples["DashBoard"][cExample]:=&("{||"+cExample+"()}")
        case (cRight$"012|013|014")
            jExamples["Nav"][cExample]:=&("{||"+cExample+"()}")
        case (cRight$"015")
            jExamples["DumpDOM"][cExample]:=&("{||"+cExample+"()}")
        case (cRight$"016")
            jExamples["iFrame"][cExample]:=&("{||"+cExample+"()}")
        endcase
    next nExample

    oWNDDefault:=GetWNDDefault()
    if ((Empty(oWNDDefault)).and.(Type("oMainWnd")=="O"))
        oWNDDefault:=oMainWnd
    endif

    oFWDefSize:=FWDefSize():New(.T.,.T.)//Sem enchoicebar
    oFWDefSize:lLateral:=.F.//Calculo vertical
    oFWDefSize:AddObject("TMENU",100,100,.T.,.T.)//TMENU

    oFWDefSize:Process()

    nTop:=oFWDefSize:aWindSize[1]
    nLeft:=oFWDefSize:aWindSize[2]
    nBottom:=oFWDefSize:aWindSize[3]
    nRight:=oFWDefSize:aWindSize[4]

    if (lRedefineBottom)
        nBottom-=50
    endif

    DEFINE MSDIALOG oTDlg TITLE cCadastro FROM nTop,nLeft TO nBottom,nRight PIXEL OF oWNDDefault

        if (lRedefineBottom)
            oTDlg:nTop+=7
            oTDlg:nLeft+=5
            oTDlg:nRight+=0
            oTDlg:nBottom-=11
        endif

        @ 000,000 MSPANEL oTPanel OF oTDlg
        oTPanel:Align:=CONTROL_ALIGN_ALLCLIENT

        @ nTop,nLeft BITMAP oTBitMap RESNAME "DNA_TECH_LOGO_MENU_BACKGROUND_BLACK_PANTER" SIZE nBottom,nRight BORDER OF oTPanel PIXEL
        oTBitMap:lTransparent:=.F.
        oTBitMap:Align:=CONTROL_ALIGN_ALLCLIENT
        oTBitMap:SetCSS("{background-size: 100% 100%;}")

        FWMsgRun(nil,{||oTMenu:=LoadMenu(@oTDlg,jExamples)},"Wait! Loading Menu",cCadastro)

        oTDlg:SetMenu(oTMenu)
        oTDlg:lCentered:=(!lRedefineBottom)

    ACTIVATE DIALOG oTDlg

    WebFileTools():ObliterateFWWebExTmpFiles(.T.)

    FreeObj(@jExamples)

    FreeObj(@oFWDefSize)

    FreeObj(@oTBitMap)
    FreeObj(@oTPanel)
    FreeObj(@oTMenu)
    FreeObj(@oTDlg)

return

static function LoadMenu(oTDlg as object,jExamples as json) as object

    local aNames as array
    local aCategories as array

    local bTMenuAction as codeblock

    local cName as character
    local cCategory as character

    local nName as numeric
    local nNames as numeric

    local nCategory as numeric
    local nCategories as numeric

    local oTMenu as object
    local oTMenuItem as object

    aCategories:=aClone(jExamples:GetNames())
    nCategories:=Len(aCategories)

    MENU oTMenu IMAGE "" OF oTDlg
        MENUITEM oTMenuItem PROMPT cCadastro
        MENU
            for nCategory:=1 to nCategories
                cCategory:=aCategories[nCategory]
                MENUITEM oTMenuItem PROMPT cCategory
                MENU
                    aNames:=aClone(jExamples[cCategory]:GetNames())
                    nNames:=Len(aNames)
                    for nName:=1 to nNames
                        cName:=aNames[nName]
                        bTMenuAction:=jExamples[cCategory][cName]
                        MENUITEM oTMenuItem PROMPT cName
                        oTMenuItem:bAction:=bTMenuAction
                    next nName
                    FWFreeArray(@aNames)
                ENDMENU
            next nCategory
        ENDMENU
        MENU
            MENUITEM oTMenuItem PROMPT "Exit" ACTION if(ApMsgNoYes("Deseja sair?"),oTDlg:End(),.F.)
        ENDMENU
    ENDMENU

    oTMenu:align:=CONTROL_ALIGN_ALLCLIENT

    FWFreeArray(@aNames)
    FWFreeArray(@aCategories)

return(oTMenu)
