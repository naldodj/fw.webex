/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

+---> WebExPage ("Drill-down Exemplo")
      |
      +---> WebExMain
            |
            +---> WebExContainer
                  |
                  +---> WebExTable ("Turnover por Empresa")
                        |
                        +---> <thead>
                        |     |
                        |     +---> <tr>
                        |           +---> <th>Codigo</th>
                        |           +---> <th>Nome</th>
                        |
                        +---> <tbody>
                              |
                              +---> <tr> (Empresa 99)
                              |     |
                              |     +---> <td>99</td>
                              |     +---> <td>TOTVS</td>
                              |
                              |     +---> DrillDown HTML (escaped)
                              |           |
                              |           +---> WebExTable ("Turnover Departamento [99-0001]")
                              |                 |
                              |                 +---> <thead>
                              |                 |     |
                              |                 |     +---> <tr>
                              |                 |           +---> <th>Codigo</th>
                              |                 |           +---> <th>Nome</th>
                              |                 |           +---> <th>% Turnover</th>
                              |                 |
                              |                 +---> <tbody>
                              |                       |
                              |                       +---> <tr>
                              |                             +---> <td>99-0001</td>
                              |                             +---> <td>DEPARTAMENTO 0001</td>
                              |                             +---> <td>50%</td>
                              |
                              +---> <tr> (Empresa 01)
                                    |
                                    +---> <td>01</td>
                                    +---> <td>DNATech</td>
                                    |
                                    +---> DrillDown HTML (escaped)
                                          |
                                          +---> WebExTable ("Turnover Departamento [01-0001]")
                                                |
                                                +---> <thead>
                                                |     |
                                                |     +---> <tr>
                                                |           +---> <th>Codigo</th>
                                                |           +---> <th>Nome</th>
                                                |           +---> <th>% Turnover</th>
                                                |
                                                +---> <tbody>
                                                      |
                                                      +---> <tr>
                                                            +---> <td>01-0001</td>
                                                            +---> <td>DEPARTAMENTO 0001</td>
                                                            +---> <td>.5%</td>

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

#include "fw.webex.th"

using namespace FWWebEx

procedure u_FWWebExExample_009()
    local bExecute as codeblock
    local cHTML as character
    local cHTMLFile as character
    local cProcName:=ProcName() as character
    bExecute:={||FWMsgRun(nil,{||cHTMLFile:=FWWebExExample_009(@cHTML)},"Aguarde",cProcName)}
    FWExampleTools():Execute(bExecute,cProcName,.T.,.T.)
    if (!Empty(cHTMLFile).and.File(cHTMLFile))
        FWExampleTools():htmlFileShow(cHTML,cProcName,cHTMLFile)
        fErase(cHTMLFile)
    endif
return

static procedure FWWebExExample_009(cHTML as character) as character

    local aEmpresas:={;
         {;
            "99";
           ,"TOTVS";
          ,{;
                {;
                     "99-0001";
                    ,"DEPARTAMENTO 0001";
                    ,"50%";
                };
            };
        };
        ,{;
            "01";
           ,"DNATech";
          ,{;
                {;
                     "01-0001";
                    ,"DEPARTAMENTO 0001";
                    ,".5%";
                };
            };
        };
    } as array

    local cProcName:=ProcName() as character
    local cHTMLFile:=cProcName as character
    local cHTMLDrillDown as character

    local nEmpresa as numeric
    local nEmpresas:=Len(aEmpresas) as numeric

    local oFWWebExMain as object
    local oFWWebExBody as object
    local oFWWebExPage as object
    local oFWWebExTable as object
    local oFWWebExContainer as object
    local oFWWebExTableDrillDown as object

    oFWWebExTable:=WebExTable():New("Turnover por Empresa")
    oFWWebExTable:SetTitleBefore(.T.)
    oFWWebExTable:EnableDrillDown()

    oFWWebExTable:AddColumnHeader("C&oacute;digo")
    oFWWebExTable:AddColumnHeader("Nome")
    oFWWebExTable:BuildHeader()

    for nEmpresa:=1 to nEmpresas
        oFWWebExTableDrillDown:=WebExTable():New()
        oFWWebExTableDrillDown:SetTitleBefore(.T.)
        oFWWebExTableDrillDown:AddColumnHeader("C&oacute;digo")
        oFWWebExTableDrillDown:AddColumnHeader("Nome")
        oFWWebExTableDrillDown:AddColumnHeader("% Turnover")
        oFWWebExTableDrillDown:BuildHeader()
        aEval(aEmpresas[nEmpresa][3],{|aRowDrillDown|;
                 oFWWebExTableDrillDown:SetTitle("Turnover Departamento ["+aRowDrillDown[1]+"]");
                ,oFWWebExTableDrillDown:AddCell(aRowDrillDown[1]);
                ,oFWWebExTableDrillDown:AddCell(aRowDrillDown[2]);
                ,oFWWebExTableDrillDown:AddCell(aRowDrillDown[3]);
                ,oFWWebExTableDrillDown:BuildBodyRow();
            };
        )
        cHTMLDrillDown:=oFWWebExTableDrillDown:RenderHTML()
        cHTMLDrillDown:=WebExHelper():EscapeAll(cHTMLDrillDown)
        FreeObj(@oFWWebExTableDrillDown)
        oFWWebExTable:AddCell(aEmpresas[nEmpresa][1])
        oFWWebExTable:AddCell(aEmpresas[nEmpresa][2])
        oFWWebExTable:BuildBodyRow(cHTMLDrillDown)
    next nEmpresa

    oFWWebExContainer:=WebExContainer():New()
    oFWWebExContainer:AddChild(oFWWebExTable)

    oFWWebExMain:=WebExMain():New()
    oFWWebExMain:AddChild(oFWWebExContainer)

    oFWWebExBody:=WebExBody():New()
    oFWWebExBody:AddChild(oFWWebExMain)

    oFWWebExPage:=WebExPage():New("Drill-down Exemplo")
    oFWWebExPage:AddChild(oFWWebExBody)

    WebFileTools():HTMLFromControl(oFWWebExPage,oFWWebExPage:GetFWWebExTmpPath(),@cHTMLFile,@cHTML,.T.)

    oFWWebExPage:Clean()

    FreeObj(@oFWWebExMain)
    FreeObj(@oFWWebExBody)
    FreeObj(@oFWWebExPage)
    FreeObj(@oFWWebExTable)
    FreeObj(@oFWWebExContainer)
    FreeObj(@oFWWebExTableDrillDown)

    FWFreeArray(@aEmpresas)

return(cHTMLFile)
