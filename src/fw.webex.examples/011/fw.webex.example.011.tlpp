/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

+---> WebExPage ("FWWebExExample_011 (KPI Dashboard)")
      |
      +---> WebExRow (Primeira linha de KPIs)
      |     |
      |     +---> WebExCol (col-4)
      |     |     |
      |     |     +---> WebExCardKPI ("Faturamento", "R$ 125.000", bg-success)
      |     |           |
      |     |           +---> WebExIcon ("bi-bar-chart")
      |     |
      |     +---> WebExCol (col-4, border-start, border-secondary, ps-3)
      |     |     |
      |     |     +---> WebExCardKPI ("Despesas", "R$ 87.000", bg-danger)
      |     |           |
      |     |           +---> WebExIcon ("bi-graph-up")
      |     |
      |     +---> WebExCol (col-4, border-start, border-secondary, ps-3)
      |           |
      |           +---> WebExCardKPI ("Outros", "R$ 75.000", bg-info)
      |                 |
      |                 +---> WebExIcon ("bi-currency-dollar")
      |
      +---> WebExHR (linha horizontal separadora)
      |
      +---> WebExRow (Segunda linha de KPIs)
            |
            +---> WebExCol (col-6)
            |     |
            |     +---> WebExCardKPI ("Clientes Ativos", "1.024", bg-info)
            |           |
            |           +---> WebExIcon ("analytics", material)
            |           +---> Icone depois do texto (lSetIconBefore(.F.))
            |
            +---> WebExCol (col-4, border-start, border-secondary, ps-3)
                  |
                  +---> WebExCardKPI ("Ticket Medio", "R$ 122,00", bg-warning)
                        |
                        +---> WebExIcon ("insights", material)
                        +---> Icone depois do texto (lSetIconBefore(.F.))

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

#include "fw.webex.th"

using namespace FWWebEx

procedure u_FWWebExExample_011()
    local bExecute as codeblock
    local cHTML as character
    local cHTMLFile as character
    local cProcName:=ProcName() as character
    local oTWebChannel:=WebApp():GetTWebChannel() as object
    bExecute:={||FWMsgRun(nil,{||cHTMLFile:=FWWebExExample_011(@cHTML)},"Aguarde",cProcName)}
    FWExampleTools():Execute(bExecute,cProcName,.T.)
    if (!Empty(cHTMLFile).and.File(cHTMLFile))
        FWExampleTools():htmlFileShow(cHTML,cProcName,cHTMLFile,oTWebChannel)
        fErase(cHTMLFile)
    endif
    WebFileTools():ObliterateFWWebExTmpFiles(.T.)
return

static procedure FWWebExExample_011(cHTML as character) as character

    local cProcName:=ProcName() as character
    local cHTMLFile:=cProcName as character

    local oFWWebExPage as object

    WITH WEBEXOBJECT oFWWebExPage CLASS WebExPage ARGS cProcName+" (KPI Dashboard)"
        //Body
        WITH WEBEXOBJECT CLASS WebExBody
            //Main
            WITH WEBEXOBJECT CLASS WebExMain
                // ROW 1
                WITH WEBEXOBJECT CLASS WebExRow
                    // KPI 1
                    WITH WEBEXOBJECT CLASS WebExCol ARGS 4
                        WITH WEBEXOBJECT CLASS WebExCardKPI ARGS "Faturamento","R$ 125.000","bg-success",WebExIcon():New("bi-bar-chart")
                        END WEBEXOBJECT
                    END WEBEXOBJECT
                    // KPI 2
                    WITH WEBEXOBJECT CLASS WebExCol ARGS 4
                        .:AddClass("border-start")
                        .:AddClass("border-secondary")
                        .:AddClass("ps-3")
                        WITH WEBEXOBJECT CLASS WebExCardKPI ARGS "Despesas","R$ 87.000","bg-danger",WebExIcon():New("bi-graph-up")
                        END WEBEXOBJECT
                    END WEBEXOBJECT
                    // KPI 3
                    WITH WEBEXOBJECT CLASS WebExCol ARGS 4
                        .:AddClass("border-start")
                        .:AddClass("border-secondary")
                        .:AddClass("ps-3")
                        WITH WEBEXOBJECT CLASS WebExCardKPI ARGS "Outros","R$ 75.000","bg-info",WebExIcon():New("bi-currency-dollar")
                        END WEBEXOBJECT
                    END WEBEXOBJECT
                END WEBEXOBJECT
                WITH WEBEXOBJECT CLASS WebExHR() // separador horizontal
                END WEBEXOBJECT
                // ROW 2
                WITH WEBEXOBJECT CLASS WebExRow
                    // KPI 1
                    WITH WEBEXOBJECT CLASS WebExCol ARGS 6
                        WITH WEBEXOBJECT CLASS WebExCardKPI ARGS "Clientes Ativos","1.024","bg-info",WebExIcon():New("analytics","material",.F.)
                        END WEBEXOBJECT
                    END WEBEXOBJECT
                    // KPI 2
                    WITH WEBEXOBJECT CLASS WebExCol ARGS 4
                        .:AddClass("border-start")
                        .:AddClass("border-secondary")
                        .:AddClass("ps-3")
                        WITH WEBEXOBJECT CLASS WebExCardKPI ARGS "Ticket M&eacute;dio","R$ 122,00","bg-warning",WebExIcon():New("insights","material",.F.)
                        END WEBEXOBJECT
                    END WEBEXOBJECT
                END WEBEXOBJECT
            END WEBEXOBJECT
        END WEBEXOBJECT
    END WEBEXOBJECT

    WebFileTools():HTMLFromControl(oFWWebExPage,oFWWebExPage:GetFWWebExTmpPath(),@cHTMLFile,@cHTML,.T.)

    WEBEXOBJECT CLEAN

return(cHTMLFile)
