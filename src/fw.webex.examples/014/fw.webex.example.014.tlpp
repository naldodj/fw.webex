/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

+---> WebExPage
      |
      +---> oWrapper (div.d-flex)
          |
          +---> oSidebar (ul.nav.flex-column.bg-primary.p-3)
          |    |
          |    +---> oBrandLink (a.d-flex.align-items-center.justify-content-center.mb-3.text-white.fw-bold.text-decoration-none)
          |    |     |
          |    |     +---> oBrandIcon (div.me-2)
          |    |     |     |
          |    |     |     +---> WebExIcon (bi-bar-chart)
          |    |     |
          |    |     +---> oBrandText (div.mx-3)
          |    |           |
          |    |           +---> oTextInBrand (span.fw-bold com texto "FWWebEx")
          |    |
          |    +---> oFWWebExHRDivider (hr.my-2)
          |    |
          |    +---> oMenuItem (li.nav-item.active)
          |          |
          |          +---> oMenuLink (a.nav-link[href=#])
          |                |
          |                +---> oMenuIcon (WebExIcon.bi-speedometer2)
          |                +---> oMenuText (span com texto "Dashboard")
          |
          +---> oContentWrapper (div.d-flex.flex-column.w-100)
                |
                +---> oTopBar (nav.navbar.shadow.mb-3)
                |     |
                |     +---> oH1 (h1.h4 com texto "Dashboard")
                |
                +---> oFWWebExMain
                      |
                      +---> oFWWebExContainer (container-fluid)
                            |
                            +---> WebExCardKPI "Earnings (Monthly)"
                            +---> WebExCardKPI "Earnings (Annual)"
                            +---> WebExCardKPI "Tasks"
                            +---> WebExCardKPI "Pending Requests"

Released to Public Domain.
Modulo FWSBAdmin - componentes prontos baseados no template SB Admin 2
--------------------------------------------------------------------------------------
*/

#include "fw.webex.th"

using namespace FWWebEx

procedure u_FWWebExExample_014()
    local bExecute as codeblock
    local cHTML as character
    local cHTMLFile as character
    local cProcName:=ProcName() as character
    local oTWebChannel:=WebApp():GetTWebChannel() as object
    bExecute:={||FWMsgRun(nil,{||cHTMLFile:=FWWebExExample_014(@cHTML)},"Aguarde",cProcName)}
    FWExampleTools():Execute(bExecute,cProcName,.T.)
    if (!Empty(cHTMLFile).and.File(cHTMLFile))
        FWExampleTools():htmlFileShow(cHTML,cProcName,cHTMLFile,oTWebChannel)
        fErase(cHTMLFile)
    endif
    WebFileTools():ObliterateFWWebExTmpFiles(.T.)
return

// Dashboard basico com layout SBAdmin
static function FWWebExExample_014(cHTML as character) as character

    local cProcName:=ProcName() as character
    local cHTMLFile:=cProcName as character

    local oH1 as object
    local oTopBar as object
    local oWrapper as object
    local oSidebar as object

    local oTextInBrand as object

    local oBrandLink as object
    local oBrandIcon as object
    local oBrandText as object

    local oMenuItem as object
    local oMenuLink as object
    local oMenuIcon as object
    local oMenuText as object

    local oFWWebExBody as object
    local oFWWebExMain as object
    local oFWWebExPage as object
    local oContentWrapper as object
    local oFWWebExHRDivider as object
    local oFWWebExContainer as object

    oBrandIcon:=WebExControl():New("div")
    oBrandIcon:AddClass("me-2")
    oBrandIcon:AddChild(WebExIcon():New("bi-bar-chart"))

    oTextInBrand:=WebExControl():New("span")
    oTextInBrand:AddClass("fw-bold")
    oTextInBrand:SetContent("FWWebEx")

    oBrandText:=WebExControl():New("div")
    oBrandText:AddClass("mx-3")
    oBrandText:AddChild(oTextInBrand)

    // Brand
    oBrandLink:=WebExControl():New("a")
    oBrandLink:AddClass("d-flex align-items-center justify-content-center mb-3 text-white fw-bold text-decoration-none")
    oBrandLink:SetAttr("href","#")
    oBrandLink:AddChild(oBrandIcon)
    oBrandLink:AddChild(oBrandText)

    // Itens de menu
    oFWWebExHRDivider:=WebExHR():New("hr")
    oFWWebExHRDivider:DelClass("my-4")
    oFWWebExHRDivider:AddClass("my-2") // Bem Bootstrap

    oMenuIcon:=WebExIcon():New("bi-speedometer2")

    oMenuText:=WebExControl():New("span")
    oMenuText:SetContent("Dashboard")

    oMenuLink:=WebExControl():New("a")
    oMenuLink:AddClass("nav-link"):SetAttr("href","#")
    oMenuLink:AddChild(oMenuIcon)
    oMenuLink:AddChild(oMenuText)

    oMenuItem:=WebExControl():New("li")
    oMenuItem:AddClass("nav-item active")
    oMenuItem:AddChild(oMenuLink)

    oSidebar:=WebExControl():New("ul")
    oSidebar:AddClass("nav flex-column bg-primary p-3")
    oSidebar:AddChild(oBrandLink)
    oSidebar:AddChild(oFWWebExHRDivider)
    oSidebar:AddChild(oMenuItem)

    oH1:=WebExControl():New("h1")
    oH1:AddClass("h4")
    oH1:SetContent("Dashboard")

    oTopBar:=WebExControl():New("nav")
    oTopBar:AddClass("navbar shadow mb-3")
    oTopBar:AddChild(oH1)

    oFWWebExMain:=WebExMain():New()
    oFWWebExContainer:=WebExContainer():New(.T.)

    // Cards
    oFWWebExContainer:AddChild(WebExCardKPI():New("Earnings (Monthly)","$40,000","bg-primary",WebExIcon():New("bi-calendar")))
    oFWWebExContainer:AddChild(WebExCardKPI():New("Earnings (Annual)","$215,000","bg-success",WebExIcon():New("bi-cash")))
    oFWWebExContainer:AddChild(WebExCardKPI():New("Tasks","50%","bg-info",WebExIcon():New("bi-clipboard")))
    oFWWebExContainer:AddChild(WebExCardKPI():New("Pending Requests","18","bg-warning",WebExIcon():New("bi-chat")))

    oFWWebExMain:AddChild(oFWWebExContainer)

    // Conteudo principal
    oContentWrapper:=WebExControl():New("div")
    oContentWrapper:AddClass("d-flex flex-column w-100")
    oContentWrapper:AddChild(oTopBar)
    oContentWrapper:AddChild(oFWWebExMain)

    oWrapper:=WebExControl():New("div")
    oWrapper:AddClass("d-flex")
    oWrapper:AddChild(oSidebar)
    oWrapper:AddChild(oContentWrapper)

    oFWWebExBody:=WebExBody():New()
    oFWWebExBody:AddChild(oWrapper)

    oFWWebExPage:=WebExPage():New("FWWebEx SBAdmin Clone")
    oFWWebExPage:AddChild(oFWWebExBody)

    WebFileTools():HTMLFromControl(oFWWebExPage,oFWWebExPage:GetFWWebExTmpPath(),@cHTMLFile,@cHTML,.T.)

    oFWWebExPage:Clean()

    FreeObj(@oH1)
    FreeObj(@oTopBar)
    FreeObj(@oWrapper)
    FreeObj(@oSidebar)

    FreeObj(@oTextInBrand)

    FreeObj(@oBrandLink)
    FreeObj(@oBrandIcon)
    FreeObj(@oBrandText)

    FreeObj(@oMenuItem)
    FreeObj(@oMenuLink)
    FreeObj(@oMenuIcon)
    FreeObj(@oMenuText)

    FreeObj(@oFWWebExMain)
    FreeObj(@oFWWebExPage)
    FreeObj(@oContentWrapper)
    FreeObj(@oFWWebExHRDivider)
    FreeObj(@oFWWebExContainer)

return(cHTMLFile)
