/*

  __                            _                          _                 _             _
 / _|__      ____      __  ___ | |__    ___ __  __    ___ | |__    __ _   __| | _ __ ___  (_) _ __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /   / __|| '_ \  / _` | / _` || '_ ` _ \ | || '_ \
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <  _ \__ \| |_) || (_| || (_| || | | | | || || | | |
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\(_)|___/|_.__/  \__,_| \__,_||_| |_| |_||_||_| |_|

Released to Public Domain.
--------------------------------------------------------------------------------------
*/
namespace FWWebEx

#include "fw.webex.th"

class SBAdminCardContainer from WebExDiv

    protected data nXLSize as numeric
    protected data nMDSize as numeric

    public method New(nXLSize as numeric,nMDSize as numeric) as object

    public method SetXLSize(nXLSize as numeric) as object
    public method SetMDSize(nMDSize as numeric) as object

endclass

method New(nXLSize,nMDSize) class SBAdminCardContainer

    paramtype 1 var nXLSize as numeric optional default 3
    paramtype 2 var nMDSize as numeric optional default 6

    _Super:New()

    ::nXLSize:=nXLSize
    ::nMDSize:=nMDSize

    ::SetXLSize()
    ::SetMDSize()

return(self)

method SetXLSize(nXLSize) class SBAdminCardContainer
    local cXLSize as character
    paramtype 1 var nXLSize as numeric optional default ::nXLSize
    nXLSize:=Min(Max(Int(nXLSize),1),12)
    cXLSize:=LTrim(Str(nXLSize))
    ::DelClassRegex("col-xl-[0-9]+") // remove qualquer col-xl-* anterior (se tiver)
    ::AddClass("col-xl-"+cXLSize)
return(self)

method SetMDSize(nMDSize) class SBAdminCardContainer
    local cMDSize as character
    paramtype 1 var nMDSize as numeric optional default ::nMDSize
    nMDSize:=Min(Max(Int(nMDSize),1),12)
    cMDSize:=LTrim(Str(nMDSize))
    ::DelClassRegex("col-md-[0-9]+") // remove qualquer col-md-* anterior (se tiver)
    ::AddClass("col-md-"+cMDSize)
return(self)
