/*

  __                            _                          _                 _             _
 / _|__      ____      __  ___ | |__    ___ __  __    ___ | |__    __ _   __| | _ __ ___  (_) _ __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /   / __|| '_ \  / _` | / _` || '_ ` _ \ | || '_ \
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <  _ \__ \| |_) || (_| || (_| || | | | | || || | | |
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\(_)|___/|_.__/  \__,_| \__,_||_| |_| |_||_||_| |_|

Released to Public Domain.
--------------------------------------------------------------------------------------
*/
namespace FWWebEx

#include "fw.webex.th"

class SBAdminCardWithLink from WebExDiv

    protected data cCardBody as character
    protected data cCardFooterLink as character
    protected data cCardFooterText as character

    protected data oCard as object
    protected data oCardBody as object
    protected data oCardFooter as object
    protected data oCardContainer as object
    protected data oCardFooterLink as object
    protected data oCardFooterIcon as object
    protected data oCardFooterContainerIcon as object

    public method New(cCardBody as character,cCardFooterLink as character,cCardFooterText as character) as object

    public method SetCardBody(cCardBody as character) as object
    public method SetCardFooterLink(cCardFooterLink  as character) as object
    public method SetCardFooterText(cCardFooterText as character) as object

endclass

method New(cCardBody,cCardFooterLink,cCardFooterText) class SBAdminCardWithLink

    paramtype 1 var cCardBody as character optional default ""
    paramtype 2 var cCardFooterLink as character optional default "#"
    paramtype 3 var cCardFooterText as character optional default "View Details"

    ::cCardBody:=cCardBody
    ::cCardFooterLink:=cCardFooterLink
    ::cCardFooterText:=cCardFooterText

    _Super:New()

    ::oCard:=self

    WITH WEBEXOBJECT ::oCard
        .:AddClass("card mb-4")
        WITH WEBEXOBJECT ::oCardBody CLASS WebExDiv
            .:AddClass("card-body")
            ::SetCardBody()
        END WEBEXOBJECT
        WITH WEBEXOBJECT ::oCardFooter CLASS WebExDiv
            .:AddClass("card-footer d-flex align-items-center justify-content-between")
            WITH WEBEXOBJECT ::oCardFooterLink CLASS WebExAnchor
                .:AddClass("small text-white stretched-link")
                ::SetCardFooterLink()
                ::SetCardFooterText()
            END WEBEXOBJECT
            WITH WEBEXOBJECT ::oCardFooterContainerIcon CLASS WebExDiv
                ::AddClass("small text-white")
                WITH WEBEXOBJECT ::oCardFooterIcon CLASS WebExIcon ARGS "fas fa-angle-right","fontawesome"
                END WEBEXOBJECT
            END WEBEXOBJECT
        END WEBEXOBJECT
    END WEBEXOBJECT NOADDCHILD

return(self)

method SetCardBody(cCardBody) class SBAdminCardWithLink
    paramtype 1 var cCardBody as character optional default ::cCardBody
    ::cCardBody:=cCardBody
    ::oCardBody:SetContent(::cCardBody)
return(self)

method SetCardFooterLink(cCardFooterLink) class SBAdminCardWithLink
    paramtype 1 var cCardFooterLink as character optional default ::cCardFooterLink
    ::cCardFooterLink:=cCardFooterLink
    ::oCardFooterLink:SetAttr("href",::cCardFooterLink)
return(self)

method SetCardFooterText(cCardFooterText) class SBAdminCardWithLink
    paramtype 1 var cCardFooterText as character optional default ::cCardFooterText
    ::cCardFooterText:=cCardFooterText
    ::oCardFooterLink:SetContent(::cCardFooterText)
return(self)
