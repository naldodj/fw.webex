/*

  __                            _                          _                 _             _
 / _|__      ____      __  ___ | |__    ___ __  __    ___ | |__    __ _   __| | _ __ ___  (_) _ __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /   / __|| '_ \  / _` | / _` || '_ ` _ \ | || '_ \
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <  _ \__ \| |_) || (_| || (_| || | | | | || || | | |
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\(_)|___/|_.__/  \__,_| \__,_||_| |_| |_||_||_| |_|

Released to Public Domain.
--------------------------------------------------------------------------------------
*/
namespace FWWebEx

#include "fw.webex.th"

class SBAdminNav from WebExNav

    protected data cBrandName as character
    protected data cBrandLink as character
    protected data cSBAdminNavHRef as character

    protected data jSBAdminNavRightMenu as json

    protected data lNavRightMenu as logical

    protected data oSBAdminNav as object
    protected data oSBAdminNavBrand as object
    protected data oSBAdminNavToggle as object
    protected data oSBAdminNavIconToggle as object

    protected data oSBAdminNavFormSearch as object
    protected data oSBAdminNavFormSearchInput as object
    protected data oSBAdminNavFormSearchButton as object
    protected data oSBAdminNavFormSearchIconButton as object
    protected data oSBAdminNavFormSearchInputGroup as object

    protected data oSBAdminNavRightMenu as object
    protected data oSBAdminNavRightMenuItem as object

    public method New(cBrandName as character,cBrandLInk as character,lNavRightMenu) as object

    public method SetBrandName(cBrandName as character) as object
    public method SetBrandLink(cBrandLInk as character) as object

    public method NavRightMenuAdd(oSBAdminNav as object,cSBAdminNavHRef as character) as object

    public method NavRightLoadJSONMenu(jSBAdminNavRightMenu as json) as object
    public method RenderDropdownMenu(aMenuItems as array,cClass as character,cAriaLabelledBy as character) as object

    public method NavRightMenuAddDivider(oSBAdminNavRightMenu as object) as object

endclass

method New(cBrandName,cBrandLink,lNavRightMenu) class SBAdminNav

    paramtype 1 var cBrandName as character optional default "FWWebEx.SBAdmin"
    paramtype 2 var cBrandLink as character optional default "#"
    paramtype 3 var lNavRightMenu as logical optional default .T.

    ::cBrandName:=cBrandName
    ::cBrandLink:=cBrandLink
    ::cSBAdminNavHRef:="#"

    ::lNavRightMenu:=lNavRightMenu

    _Super:New()
    ::SetFixedID("sb-topnav")
    ::AddClass("sb-topnav navbar navbar-expand navbar-dark bg-dark")

    ::oSBAdminNav:=self

    WITH WEBEXOBJECT ::oSBAdminNav
        WITH WEBEXOBJECT CLASS WebExComment
            .:SetContent("Navbar Brand")
        END WEBEXOBJECT
        WITH WEBEXOBJECT ::oSBAdminNavBrand CLASS WebExAnchor
            .:AddClass("navbar-brand ps-3")
            ::SetBrandLink(::cBrandLink)
            ::SetBrandName(::cBrandName)
        END WEBEXOBJECT
        WITH WEBEXOBJECT CLASS WebExComment
            .:SetContent("Sidebar Toggle")
        END WEBEXOBJECT
        WITH WEBEXOBJECT ::oSBAdminNavToggle CLASS WebExButton ARGS "",""
            .:DelAllAttr()
            .:SetFixedID("sidebarToggle")
            .:AddClass("btn btn-link btn-sm order-1 order-lg-0 me-4 me-lg-0")
            .:SetAttr("href","#!")
            WITH WEBEXOBJECT ::oSBAdminNavIconToggle CLASS WebExIcon() ARGS "fas fa-bars","fontawesome"
            END WEBEXOBJECT
        END WEBEXOBJECT
        WITH WEBEXOBJECT CLASS WebExComment
            .:SetContent("Navbar Search")
        END WEBEXOBJECT
        WITH WEBEXOBJECT ::oSBAdminNavFormSearch CLASS WebExForm ARGS ""
            .:DelAllAttr()
            .:AddClass("d-none d-md-inline-block form-inline ms-auto me-0 me-md-3 my-2 my-md-0")
            WITH WEBEXOBJECT ::oSBAdminNavFormSearchInputGroup CLASS WebExInputGroup
                WITH WEBEXOBJECT ::oSBAdminNavFormSearchInput CLASS WebExInput ARGS "btnNavbarSearch","text","Search for...","btnNavbarSearch"
                    .:SetAttr("aria-label","Search for...")
                    .:SetAttr("aria-describedby","btnNavbarSearch")
                END WEBEXOBJECT
                WITH WEBEXOBJECT ::oSBAdminNavFormSearchButton CLASS WebExButton ARGS "",""
                    .:DelAllAttr()
                    .:SetAttr("type","button")
                    .:SetFixedID("btnNavbarSearch")
                    .:AddClass("btn btn-primary")
                    WITH WEBEXOBJECT ::oSBAdminNavFormSearchIconButton CLASS WebExIcon ARGS "fas fa-search","fontawesome"
                    END WEBEXOBJECT
                END WEBEXOBJECT
            END WEBEXOBJECT
        END WEBEXOBJECT
        if (::lNavRightMenu)
            ::lNavRightMenu:=.F.
            ::NavRightMenuAdd(self)
        endif
    END WEBEXOBJECT NOADDCHILD

return(self)

method SetBrandName(cBrandName) class SBAdminNav
    paramtype cBrandName as character optional default ::cBrandName
    ::cBrandName:=cBrandName
    ::oSBAdminNavBrand:SetContent(::cBrandName)
return(self)

method SetBrandLink(cBrandLInk) class SBAdminNav
    paramtype cBrandLInk as character optional default ::cBrandLInk
    ::cBrandLInk:=cBrandLInk
    ::oSBAdminNavBrand:SetAttr("href",::cBrandLInk)
return(self)

method NavRightMenuAdd(oSBAdminNav,cSBAdminNavHRef) class SBAdminNav
    paramtype 1 var oSBAdminNav as object class SBAdminNav optional
    paramtype 2 var cSBAdminNavHRef as character optional default ::cSBAdminNavHRef
    default oSBAdminNav:=::oSBAdminNav
    if (!::lNavRightMenu)
        ::lNavRightMenu:=.T.
        WITH WEBEXOBJECT ::oSBAdminNav
            WITH WEBEXOBJECT CLASS WebExComment
                .:SetContent("Navbar")
            END WEBEXOBJECT
            WITH WEBEXOBJECT ::oSBAdminNavRightMenu CLASS WebExUL
                .:AddClass("navbar-nav ms-auto ms-md-0 me-3 me-lg-4")
                WITH WEBEXOBJECT ::oSBAdminNavRightMenuItem CLASS WebExLI
                    .:SetAttr("class","nav-item dropdown")
                    WITH WEBEXOBJECT CLASS WebExAnchor
                        .:SetAttr("class","nav-link dropdown-toggle")
                        .:SetFixedID("navbarDropdown")
                        .:SetAttr("href",::cSBAdminNavHRef)
                        .:SetAttr("role","button" )
                        .:SetAttr("data-bs-toggle","dropdown")
                        .:SetAttr("aria-expanded","false")
                        WITH WEBEXOBJECT CLASS WebExIcon() ARGS "fas fa-user fa-fw","fontawesome"
                        END WEBEXOBJECT
                    END WEBEXOBJECT
                END WEBEXOBJECT
            END WEBEXOBJECT
        END WEBEXOBJECT NOADDCHILD
    endif
return(self)

/*
{
  "menu": [
    {
      "id": "dashboard0",
      "label": "Dashboard0",
      "icon": "fas fa-tachometer-alt",
      "url": "/dashboard",
      "badge": {
        "label": "Novo",
        "class": "bg-success text-white"
      },
      "subItems": [
        {
          "id": "generalview",
          "label": "Vis&atilde;o Geral",
          "icon": "fas fa-chart-pie",
          "url": "/dashboard/overview",
          "badge": {
            "label": "Novo",
            "class": "bg-success text-white"
          },
          "roles": ["000000"],
          "subItems": []
        }
      ]
    },
    {
      "divider": true
    },
    {
      "id": "indicators",
      "label": "Indicadores",
      "icon": "fas fa-chart-line",
      "url": "ws:KPIS_REQUEST:KPIS_RESPONSE",
      "badge": {
        "label": "Novo",
        "class": "bg-success text-white"
      },
      "wsAction": {
        "class": "userRestCrudTLPPCoreFunction",
        "method": "dna.tech.codAliasPost",
        "params": {
          "codAlias": "SRA",
          "filter": {
            "RA_SEXO": "M"
          },
          "yesFields": "RA_FILIAL,RA_MAT,RA_APELIDO"
        }
      },
      "subItems": []
    }
  ]
}
*/
method NavRightLoadJSONMenu(jSBAdminNavRightMenu) class SBAdminNav

    local aMenus as array

    paramtype 1 var jSBAdminNavRightMenu as json

    ::jSBAdminNavRightMenu:=jSBAdminNavRightMenu

    if (jSBAdminNavRightMenu:HasProperty("menu"))
        aMenus:=jSBAdminNavRightMenu["menu"]
        if (Len(aMenus)>0)
            WITH WEBEXOBJECT ::oSBAdminNavRightMenuItem
                ::RenderDropdownMenu(aMenus,"dropdown-menu dropdown-menu-end","navbarDropdown")
            END WEBEXOBJECT NOADDCHILD
        endif
    endif

return(self)

method RenderDropdownMenu(aMenuItems,cClass,cAriaLabelledBy) CLASS SBAdminNav

    local aRolesUser as array

    local nMenuItem as numeric
    local nMenuItems:=Len(aMenuItems) as numeric

    local jMenuItem as json

    if (::lNavRightMenu)

        WITH WEBEXOBJECT CLASS WebExUL
            if (!Empty(cClass))
                .:AddClass(cClass)
            endif
            if (!Empty(cAriaLabelledBy))
                .:SetAttr("aria-labelledby",cAriaLabelledBy)
            endif
            for nMenuItem:=1 to nMenuItems
                jMenuItem:=aMenuItems[nMenuItem]
                if ((jMenuItem:HasProperty("divider")).and.(jMenuItem["divider"]))
                    ::NavRightMenuAddDivider(.this)
                    loop
                endif
                if ((!jMenuItem:HasProperty("label")).or.(!jMenuItem:HasProperty("url")))
                    loop
                endif
                if ((!jMenuItem:HasProperty("roles")).and.(!Empty(jMenuItem["roles"])))
                    if (!(WebExUser():ShouldDisplayItem(jMenuItem["roles"],@aRolesUser)))
                        loop
                    endif
                endif
                WITH WEBEXOBJECT CLASS WebExLI
                    WITH WEBEXOBJECT CLASS WebExAnchor
                        .:AddClass("dropdown-item d-flex justify-content-between align-items-center gap-2")
                        if (jMenuItem:HasProperty("id"))
                            .:SetFixedID(jMenuItem["id"])
                        endif
                        if (jMenuItem:HasProperty("wsAction"))
                            .:SetAttr("href","#!")
                            .:SetAttr("data-wsurl",if(jMenuItem:HasProperty("url"),jMenuItem["url"],"#"))
                            .:SetAttr("data-wsaction",Encode64(jMenuItem["wsAction"]:ToJSON()))
                        else
                            .:SetAttr("href",if(jMenuItem:HasProperty("url"),jMenuItem["url"],"#"))
                        endif
                        if ((jMenuItem:HasProperty("icon").and.(!Empty(jMenuItem["icon"]))))
                            WITH WEBEXOBJECT CLASS WebExIcon ARGS jMenuItem["icon"],"fontawesome"
                            END WEBEXOBJECT
                        endif
                        WITH WEBEXOBJECT CLASS WebExLabel ARGS jMenuItem["label"],jMenuItem["label"]
                        END WEBEXOBJECT
                        if (jMenuItem:HasProperty("badge")).and.(jMenuItem["badge"]:HasProperty("label").and.(!Empty(jMenuItem["badge"]["label"])))
                            WITH WEBEXOBJECT CLASS WebExSpan
                                .:SetContent(jMenuItem["badge"]["label"])
                                .:AddClass("badge rounded-pill")
                                if ((jMenuItem["badge"]:HasProperty("class")).and.(!Empty(jMenuItem["badge"]["class"])))
                                    .:AddClass(jMenuItem["badge"]["class"])
                                endif
                            END WEBEXOBJECT
                        endif
                    END WEBEXOBJECT
                    if ((jMenuItem:HasProperty("subItems")).and.(Len(jMenuItem["subItems"])>0))
                        ::RenderDropdownMenu(jMenuItem["subItems"],"submenu dropdown-submenu",NIL)
                    endif
                END WEBEXOBJECT
            next nMenuItem
        END WEBEXOBJECT

    endif

    FWFreeArray(@aRolesUser)

return(self)

method NavRightMenuAddDivider(oSBAdminNavRightMenu) class SBAdminNav
    paramtype 1 var oSBAdminNavRightMenu as object class WebExUL optional
    WITH WEBEXOBJECT oSBAdminNavRightMenu
        WITH WEBEXOBJECT CLASS WebExLI
            WITH WEBEXOBJECT CLASS WebExHR
                .:DelAllAttr()
                .:SetAttr("class","dropdown-divider")
            END WEBEXOBJECT
        END WEBEXOBJECT
    END WEBEXOBJECT NOADDCHILD
return(self)
